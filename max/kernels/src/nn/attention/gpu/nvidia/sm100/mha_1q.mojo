# ===----------------------------------------------------------------------=== #
# Copyright (c) 2026, Modular Inc. All rights reserved.
#
# Licensed under the Apache License v2.0 with LLVM Exceptions:
# https://llvm.org/LICENSE.txt
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ===----------------------------------------------------------------------=== #

"""
Implements single-query (decode) multi-head attention for NVIDIA SM100 (Blackwell) GPUs using warp-specialized UMMA and tensor-memory (TMEM) accumulators.
"""

from std.math import ceildiv, exp2, recip, align_up
from std.math.uutils import umod
from std.math.constants import log2e

from std.sys import align_of, simd_width_of, size_of

import std.gpu.primitives.warp as warp
from max.gpu.primitives.grid_controls import (
    PDLLevel,
    launch_dependent_grids,
    pdl_launch_attributes,
    wait_on_dependent_grids,
)
from std.collections import OptionalReg
from std.gpu import (
    MAX_THREADS_PER_BLOCK_METADATA,
    WARP_SIZE,
    block_dim,
    lane_id,
    thread_idx,
    warp_id,
)
from max.gpu.sync import barrier
from max.gpu.host import DeviceContext, FuncAttribute, DeviceBuffer
from max.gpu.host.nvidia.tma import TensorMapSwizzle
from max.gpu.host.info import B200
from std.gpu.intrinsics import warpgroup_reg_alloc, warpgroup_reg_dealloc
from max.gpu.memory import external_memory, fence_async_view_proxy
from max.gpu.compute.mma import MMAOperandDescriptor
from max.gpu.compute.arch.mma_nvidia_sm100 import (
    MMASmemDescriptor,
    UMMAInsDescriptor,
    UMMAKind,
    mma,
    mma_arrive,
)
from max.gpu.sync import named_barrier
from max.gpu.compute.arch.tcgen05 import (
    tcgen05_alloc,
    tcgen05_dealloc,
    tcgen05_fence_after,
    tcgen05_fence_before,
    tcgen05_ld,
    tcgen05_load_wait,
    tcgen05_release_allocation_lock,
    tcgen05_st,
    tcgen05_store_wait,
)
from layout import IntTuple, Layout, LayoutTensor
from layout.layout_tensor import copy_local_to_shared, copy_sram_to_dram
from layout.swizzle import make_swizzle
from layout.tensor_core_async import (
    tile_layout_k_major_typed,
    tile_layout_mn_major_typed,
    tile_to_descriptor,
)
from layout.tma_async import PipelineState, SharedMemBarrier
from std.logger import Logger
from std.memory import bitcast
from nn.attention.mha_operand import kv_sub_tile_rows as _kv_sub_tile_rows
from nn.attention.gpu.nvidia.sm90.attention import (
    _apply_mask,
    _get_position,
    get_q_head_idx,
    produce,
)
from nn.attention.gpu.nvidia.common import (
    ImmutTileTensor1D,
    KVTMATile,
    MHAPosition,
    NonNullPointer,
    NullPointer,
    OptionalPointer,
    Pack,
    q_tma,
    QTMATile,
)
from nn.attention.mha_mask import MHAMask, TileMaskStatus
from nn.attention.mha_operand import MHAOperand
from nn.attention.gpu.nvidia.mha_tile_scheduler import (
    MHATileScheduler,
    MHATileState,
    MHATileSummary,
    SeqInfo,
    TransientScheduler,
)
from nn.attention.mha_utils import (
    FlashAttentionAlgorithm,
    MHA_PDL_LEVEL,
    MHAConfig,
    MHAPartitionScheme,
    OptionallyStaticInt,
    _is_decoding,
)
from nn.softmax import (
    _online_softmax_correction,
    _rowmax_online_softmax,
    _rowsum,
)

from std.utils.index import Index
from std.utils.numerics import get_accum_type, min_or_neg_inf
from std.utils.static_tuple import StaticTuple

comptime logger = Logger()


struct RegisterAccumulatorDescription:
    """
    Holds the number of MMA fragments and the per-thread fragment size for a register-resident accumulator tile.
    """

    var num_mmas: Int
    var frag_size: Int

    @always_inline
    def __init__(out self, num_mmas: Int, frag_size: Int):
        self.num_mmas = num_mmas
        self.frag_size = frag_size


# consumer_group_size equals
# sm90: 128 (warp group size)
# sm100: num_softmax_threads
struct RegisterAccumulatorLayout[
    MMA_M: Int,
    MMA_N: Int,
    num_m_mmas: Int,
    num_n_mmas: Int,
    consumer_group_size: Int,
    *,
    frag_simdwidth: Int = 2,
](TrivialRegisterPassable):
    """
    Describes how UMMA accumulator fragments are distributed across the threads of a consumer warp group.

    Parameters:
        MMA_M: The M dimension of a single UMMA instruction tile.
        MMA_N: The N dimension of a single UMMA instruction tile.
        num_m_mmas: Number of UMMA tiles along the M dimension.
        num_n_mmas: Number of UMMA tiles along the N dimension.
        consumer_group_size: Number of threads in the consumer warp group.
        frag_simdwidth: SIMD width of each accumulator fragment (defaults to 2).
    """

    comptime frag_size: Int = Self.MMA_M * Self.MMA_N // Self.consumer_group_size
    comptime num_row_blocks_per_mma = 2
    comptime element_layout: Layout = Layout.row_major(1, Self.frag_simdwidth)
    comptime rows_of_frags_layout: Layout = Layout.row_major(
        Self.num_m_mmas * Self.num_n_mmas, Self.frag_size
    )
    comptime vec_output_layout: Layout = Layout(
        IntTuple(
            IntTuple(Self.num_row_blocks_per_mma, Self.num_m_mmas),
            IntTuple(
                Self.frag_size
                // (Self.num_row_blocks_per_mma * Self.frag_simdwidth),
                Self.num_n_mmas,
            ),
        ),
        IntTuple(
            IntTuple(Self.frag_simdwidth, Self.frag_size),
            IntTuple(
                Self.num_row_blocks_per_mma * Self.frag_simdwidth,
                Self.num_m_mmas * Self.frag_size,
            ),
        ),
    )

    @staticmethod
    @always_inline
    def description() -> RegisterAccumulatorDescription:
        comptime assert Self.vec_output_layout.size() > 0, "layout: " + String(
            Self.vec_output_layout
        )

        return RegisterAccumulatorDescription(
            Self.num_m_mmas * Self.num_n_mmas, Self.frag_size
        )


struct MMAOperandOffsetFn[
    dtype: DType,
    BMN: Int,
    BK: Int,
    swizzle: TensorMapSwizzle,
    is_k_major: Bool,
    WMMA_MN: Int,
    WMMA_K: Int,
](TrivialRegisterPassable):
    """
    Computes the shared-memory layout and byte offsets for MMA operand tiles, bridging typed tile layouts to legacy MMA descriptors.

    Parameters:
        dtype: The element type of the operand tile.
        BMN: The non-K dimension of the operand tile in elements.
        BK: The K dimension of the operand tile in elements.
        swizzle: The shared-memory swizzle mode applied to the operand layout.
        is_k_major: Whether the operand is stored K-major versus M/N-major.
        WMMA_MN: The M or N dimension of a single warp-level MMA tile.
        WMMA_K: The K dimension of a single warp-level MMA tile.
    """

    # Use typed layouts as source of truth; bridge to legacy Layout for
    # LayoutTensor and MMA descriptor pipeline.
    comptime layout = tile_layout_k_major_typed[
        Self.dtype, Self.BMN, Self.BK, Self.swizzle
    ].to_layout() if Self.is_k_major else tile_layout_mn_major_typed[
        Self.dtype, Self.BMN, Self.BK, Self.swizzle
    ].to_layout()
    comptime layout_size: Int = Self.layout.size()

    comptime canonical_K = Self.swizzle.bytes() // size_of[
        Self.dtype
    ]() if Self.swizzle != TensorMapSwizzle.SWIZZLE_NONE else Self.BK
    comptime canonical_layout_flat = tile_layout_k_major_typed[
        Self.dtype, Self.BMN, Self.canonical_K, Self.swizzle
    ].to_layout() if Self.is_k_major else Self.layout
    comptime canonical_layout = tile_to_descriptor[
        Self.dtype, Self.canonical_layout_flat, Self.is_k_major
    ]()
    comptime canonical_layout_size = Self.canonical_layout.size()

    @always_inline
    def __init__(out self):
        pass


trait DescriptorPair(TrivialRegisterPassable):
    """
    Provides access to the A and B operand descriptors for a shared-shared (SS) UMMA operation.
    """

    comptime a_t: MMAOperandDescriptor
    comptime b_t: MMAOperandDescriptor

    @always_inline
    def get_a(self) -> Self.a_t:
        ...

    @always_inline
    def get_b(self) -> Self.b_t:
        ...


trait WriteableMMAOperandDescriptor(TrivialRegisterPassable):
    """
    Describes an MMA operand whose source data can be written from a local LayoutTensor into the operand's backing store (e.g. tensor memory).
    """

    @always_inline
    def copy_from[
        src_type: DType, src_layout: Layout, src_element_layout: Layout, //
    ](
        self,
        src: LayoutTensor[
            src_type,
            src_layout,
            MutAnyOrigin,
            address_space=.LOCAL,
            element_layout=src_element_layout,
        ],
    ):
        ...


trait DescriptorPairTS(TrivialRegisterPassable):
    """
    Provides access to the A (tensor-memory) and B (shared-memory) operand descriptors for a tensor-shared (TS) UMMA operation.
    """

    comptime a_t: WriteableMMAOperandDescriptor
    comptime b_t: MMAOperandDescriptor

    @always_inline
    def get_a(self) -> Self.a_t:
        ...

    @always_inline
    def get_b(self) -> Self.b_t:
        ...


def local_tensor_type[
    dtype: DType, layout: Layout, element_layout: Layout
](
    out dummy_arg: LayoutTensor[
        dtype,
        layout,
        MutAnyOrigin,
        address_space=.LOCAL,
        element_layout=element_layout,
    ]
):
    """
    Returns an uninitialized local-address-space LayoutTensor used for compile-time type inference of register tiles.

    Parameters:
        dtype: The element type of the inferred `LayoutTensor`.
        layout: The outer layout of the inferred `LayoutTensor`.
        element_layout: The element layout of the inferred `LayoutTensor`.
    """
    dummy_arg = {None}


trait AccumulatorTile(TrivialRegisterPassable):
    """
    Describes a UMMA accumulator tile that can be allocated, copied to and from, and viewed as rows of fragments.
    """

    comptime dtype: DType
    comptime element_layout: Layout
    comptime vec_output_layout: Layout
    comptime rows_of_frags_layout: Layout

    @staticmethod
    @always_inline
    def _empty_tensor() -> (
        type_of(
            local_tensor_type[
                Self.dtype, Self.vec_output_layout, Self.element_layout
            ]()
        )
    ):
        ...

    @staticmethod
    @always_inline
    def rows_of_frags(
        src: type_of(Self._empty_tensor()),
        out res: LayoutTensor[
            Self.dtype,
            Self.rows_of_frags_layout,
            MutAnyOrigin,
            address_space=.LOCAL,
        ],
    ):
        ...

    @staticmethod
    @always_inline
    def allocate_register_tile(
        out res: type_of(Self._empty_tensor()),
    ):
        ...

    @always_inline
    def copy_from(
        self,
        src: type_of(Self._empty_tensor()),
    ):
        ...

    @always_inline
    def copy_to(
        self,
        dst: type_of(Self._empty_tensor()),
    ):
        ...


struct UMMADescriptorSS[operand_type: DType](
    DescriptorPair, TrivialRegisterPassable
):
    """
    Holds two shared-memory descriptors for the A and B operands of a shared-shared (SS) UMMA.

    Parameters:
        operand_type: The element type of the A and B operands.
    """

    comptime operand_t = Self.operand_type
    comptime a_t = MMASmemDescriptor
    comptime b_t = MMASmemDescriptor

    var a: Self.a_t
    var b: Self.b_t

    @always_inline
    def __init__(out self, a: Self.a_t, b: Self.b_t):
        self.a = a
        self.b = b

    @always_inline
    def get_a(self) -> Self.a_t:
        return self.a

    @always_inline
    def get_b(self) -> Self.b_t:
        return self.b


@always_inline
def _tmem_offset(dtype_size: Int, *, MMA_N: Int, m_mma: Int, n_mma: Int) -> Int:
    var row = 16 * m_mma
    var col = (MMA_N * n_mma * dtype_size) // 4
    return (row << 16) + col


@always_inline
def _tmem_offset[dtype: DType, *, MMA_N: Int, m_mma: Int, n_mma: Int]() -> Int:
    comptime linear = _tmem_offset(
        size_of[dtype](), MMA_N=MMA_N, m_mma=m_mma, n_mma=n_mma
    )
    return linear


struct TMemAccumulator[
    dtype_: DType,
    MMA_M: Int,
    MMA_N: Int,
    num_m_mmas: Int,
    num_n_mmas: Int,
    num_softmax_threads: Int,
](AccumulatorTile, TrivialRegisterPassable):
    """
    Implements an `AccumulatorTile` backed by tensor memory (TMEM), storing UMMA results at a given TMEM address.

    Parameters:
        dtype_: The element type of the accumulator tile.
        MMA_M: The M dimension of a single UMMA instruction tile.
        MMA_N: The N dimension of a single UMMA instruction tile.
        num_m_mmas: Number of UMMA tiles along the M dimension.
        num_n_mmas: Number of UMMA tiles along the N dimension.
        num_softmax_threads: Number of threads in the consumer warp group.
    """

    comptime dtype: DType = Self.dtype_
    comptime layout_t = RegisterAccumulatorLayout[
        Self.MMA_M,
        Self.MMA_N,
        Self.num_m_mmas,
        Self.num_n_mmas,
        Self.num_softmax_threads,
    ]
    comptime vec_output_layout = Self.layout_t.vec_output_layout
    comptime element_layout = Self.layout_t.element_layout
    comptime rows_of_frags_layout = Self.layout_t.rows_of_frags_layout
    comptime frag_size = Self.layout_t.frag_size

    var tmem_addr: UInt32

    @always_inline
    def __init__(out self, tmem_addr: UInt32):
        Self.check_constraints()
        self.tmem_addr = tmem_addr

    @staticmethod
    @always_inline
    def _empty_tensor() -> (
        type_of(
            local_tensor_type[
                Self.dtype, Self.vec_output_layout, Self.layout_t.element_layout
            ]()
        )
    ):
        Self.check_constraints()
        return local_tensor_type[
            Self.dtype, Self.vec_output_layout, Self.layout_t.element_layout
        ]()

    @always_inline
    def __getitem__(self, i: UInt32) -> Self:
        return {self.tmem_addr + i * UInt32(Self.MMA_N)}

    @always_inline
    @staticmethod
    def check_constraints():
        comptime assert Self.vec_output_layout[0].size() > 0, (
            "layout: "
            + String(Self.vec_output_layout)
            + "\nnum_m_mmas = "
            + String(Self.num_m_mmas)
        )
        comptime assert (
            Self.vec_output_layout[1].size() > 0
        ), "layout: " + String(Self.vec_output_layout)
        comptime assert Self.MMA_M > 0, (
            "MMA_M = "
            + String(Self.MMA_M)
            + "\nMMA_N = "
            + String(Self.MMA_N)
            + "\nnum_m_mmas = "
            + String(Self.num_m_mmas)
            + "\nnum_n_mmas = "
            + String(Self.num_n_mmas)
            + "\n"
        )
        comptime assert Self.MMA_N > 0, (
            "MMA_M = "
            + String(Self.MMA_M)
            + "\nMMA_N = "
            + String(Self.MMA_N)
            + "\nnum_m_mmas = "
            + String(Self.num_m_mmas)
            + "\nnum_n_mmas = "
            + String(Self.num_n_mmas)
            + "\n"
        )
        comptime assert Self.num_m_mmas > 0, (
            "MMA_M = "
            + String(Self.MMA_M)
            + "\nMMA_N = "
            + String(Self.MMA_N)
            + "\nnum_m_mmas = "
            + String(Self.num_m_mmas)
            + "\nnum_n_mmas = "
            + String(Self.num_n_mmas)
            + "\n"
        )
        comptime assert Self.num_n_mmas > 0, (
            "MMA_M = "
            + String(Self.MMA_M)
            + "\nMMA_N = "
            + String(Self.MMA_N)
            + "\nm_mma = "
            + String(Self.num_m_mmas)
            + "\nnum_n_mmas = "
            + String(Self.num_n_mmas)
            + "\n"
        )

    @always_inline
    def offset[m_mma: Int, n_mma: Int](self) -> UInt32:
        Self.check_constraints()

        comptime if m_mma == 0 and n_mma == 0:
            return self.tmem_addr
        else:
            comptime linear = _tmem_offset[
                Self.dtype, MMA_N=Self.MMA_N, m_mma=m_mma, n_mma=n_mma
            ]()

            return self.tmem_addr + UInt32(linear)

    @staticmethod
    @always_inline
    def rows_of_frags(
        src: type_of(Self._empty_tensor()),
        out res: LayoutTensor[
            Self.dtype,
            Self.rows_of_frags_layout,
            MutAnyOrigin,
            address_space=.LOCAL,
        ],
    ):
        Self.check_constraints()
        res = {src.ptr}

    @staticmethod
    @always_inline
    def allocate_register_tile(
        out res: type_of(Self._empty_tensor()),
    ):
        res = type_of(res).stack_allocation()

    @always_inline
    def copy_from(
        self,
        src: type_of(Self._empty_tensor()),
    ):
        var frags = Self.rows_of_frags(src).vectorize[1, Self.frag_size]()
        comptime dtype_size = size_of[Self.dtype]()
        comptime assert dtype_size == 4
        comptime frag_size_b32 = Self.frag_size * dtype_size // 4
        # 16 x 256b results in repeated 8x4<1x2> pattern
        # each repetition thus fills 8 columns
        # and writes 4 values per thread.
        comptime repeat = frag_size_b32 // 4

        comptime for m_mma in range(Self.num_m_mmas):
            comptime for n_mma in range(Self.num_n_mmas):
                comptime mma_id = n_mma * Self.num_m_mmas + m_mma
                comptime tmem_offset = _tmem_offset(
                    dtype_size,
                    MMA_N=Self.MMA_N,
                    m_mma=m_mma,
                    n_mma=n_mma,
                )
                var tmem = self.tmem_addr + UInt32(tmem_offset)
                var frag = bitcast[.uint32, frag_size_b32](frags[mma_id, 0])
                # 16 x 256b results in repeated 8x4 matrix of <1,2> vector pattern
                var frag_st = Array[UInt32, frag_size_b32](uninitialized=True)

                comptime for _i in range(frag_size_b32):
                    frag_st[_i] = frag[_i]
                tcgen05_st[
                    datapaths=16,  # first dimension of the shape
                    bits=256,  # second dimension of the shape
                    repeat=repeat,
                    pack=False,
                ](tmem, frag_st)
        tcgen05_store_wait()
        tcgen05_fence_before()
        named_barrier[Int32(Self.num_softmax_threads)]()

    @always_inline
    def copy_to(
        self,
        dst: type_of(Self._empty_tensor()),
    ):
        var frags = Self.rows_of_frags(dst).vectorize[1, Self.frag_size]()
        comptime dtype_size = size_of[Self.dtype]()
        comptime assert dtype_size == 4
        comptime frag_size_b32 = (Self.frag_size * dtype_size) // 4
        # 16 x 256b results in repeated 8x4<1x2> pattern
        # each repetition thus loads 8 columns
        # and loads 4 values per thread.
        comptime repeat = frag_size_b32 // 4
        comptime assert (
            Self.num_m_mmas * Self.num_n_mmas == type_of(frags).layout.size()
        )

        comptime for m_mma in range(Self.num_m_mmas):
            comptime for n_mma in range(Self.num_n_mmas):
                comptime mma_id = n_mma * Self.num_m_mmas + m_mma
                comptime tmem_offset = _tmem_offset(
                    dtype_size,
                    MMA_N=Self.MMA_N,
                    m_mma=m_mma,
                    n_mma=n_mma,
                )
                var tmem = self.tmem_addr + UInt32(tmem_offset)
                comptime if repeat > 16:
                    # Split into two halves to reduce register pressure.
                    comptime half_repeat = repeat // 2
                    comptime half_frag = frag_size_b32 // 2
                    comptime half_col_offset = half_repeat * 8 * dtype_size // 4
                    var _ld_lo = tcgen05_ld[
                        datapaths=16,
                        bits=256,
                        repeat=half_repeat,
                        dtype=DType.uint32,
                        pack=False,
                        width=half_frag,
                    ](tmem)
                    var _ld_hi = tcgen05_ld[
                        datapaths=16,
                        bits=256,
                        repeat=half_repeat,
                        dtype=DType.uint32,
                        pack=False,
                        width=half_frag,
                    ](tmem + UInt32(half_col_offset))
                    var _ld_simd = SIMD[.uint32, frag_size_b32]()

                    comptime for _i in range(half_frag):
                        _ld_simd[_i] = _ld_lo[_i]
                        _ld_simd[_i + half_frag] = _ld_hi[_i]
                    frags[mma_id, 0] = bitcast[
                        Self.dtype, frags.element_layout.size()
                    ](_ld_simd)
                else:
                    var _ld_result = tcgen05_ld[
                        datapaths=16,
                        bits=256,
                        repeat=repeat,
                        dtype=DType.uint32,
                        pack=False,
                        width=frag_size_b32,
                    ](tmem)
                    var _ld_simd = SIMD[.uint32, frag_size_b32]()

                    comptime for _i in range(frag_size_b32):
                        _ld_simd[_i] = _ld_result[_i]
                    frags[mma_id, 0] = bitcast[
                        Self.dtype, frags.element_layout.size()
                    ](_ld_simd)

        tcgen05_load_wait()


struct TMemOperand[
    dtype: DType,
    num_m_mmas: Int,
    num_n_mmas: Int,
    MMA_M: Int,
    MMA_N: Int,
    MMA_K: Int,
    num_softmax_threads: Int,
](TrivialRegisterPassable, WriteableMMAOperandDescriptor):
    """
    Implements a `WriteableMMAOperandDescriptor` backed by tensor memory (TMEM), used as the A operand of a tensor-shared (TS) UMMA.

    Parameters:
        dtype: The element type of the operand tile.
        num_m_mmas: Number of UMMA tiles along the M dimension.
        num_n_mmas: Number of UMMA tiles along the N dimension.
        MMA_M: The M dimension of a single UMMA instruction tile.
        MMA_N: The N dimension of a single UMMA instruction tile.
        MMA_K: The K dimension of a single UMMA instruction tile.
        num_softmax_threads: Number of threads in the consumer warp group.
    """

    var tmem_addr: UInt32

    comptime reg_layout = RegisterAccumulatorLayout[
        Self.MMA_M,
        Self.MMA_N,
        Self.num_m_mmas,
        Self.num_n_mmas,
        Self.num_softmax_threads,
    ]
    comptime frag_size = Self.reg_layout.frag_size
    comptime vec_output_layout = Self.reg_layout.vec_output_layout
    comptime reg_tile_t = type_of(
        local_tensor_type[
            Self.dtype, Self.vec_output_layout, Self.reg_layout.element_layout
        ]()
    )

    @always_inline
    def __init__(out self, tmem_addr: UInt32):
        self.tmem_addr = tmem_addr

    @always_inline
    def offset[m_mma: Int, k_mma: Int](self) -> UInt32:
        comptime assert Self.MMA_M > 0, "MMA_M = " + String(Self.MMA_M) + "\n"
        comptime assert Self.MMA_K > 0, "MMA_K = " + String(Self.MMA_K) + "\n"

        comptime if m_mma == 0 and k_mma == 0:
            return self.tmem_addr
        else:
            comptime linear = _tmem_offset[
                Self.dtype, MMA_N=Self.MMA_K, m_mma=m_mma, n_mma=k_mma
            ]()
            return self.tmem_addr + UInt32(linear)

    @always_inline
    def copy_from[
        src_type: DType,
        src_layout: Layout,
        src_element_layout: Layout,
        //,
    ](
        self,
        src: LayoutTensor[
            src_type,
            src_layout,
            MutAnyOrigin,
            address_space=.LOCAL,
            element_layout=src_element_layout,
        ],
    ):
        # src has row of frags layout
        comptime num_frags = src_layout[0].size()
        comptime assert num_frags == Self.num_m_mmas * Self.num_n_mmas
        comptime assert Self.num_n_mmas == 1
        comptime assert Self.frag_size == src_layout[1].size(), (
            "Self.frag_size = "
            + String(Self.frag_size)
            + "\nsrc_layout = "
            + String(src_layout)
        )
        comptime assert src_element_layout.size() == 1
        comptime src_size = size_of[src_type]()
        comptime dst_size = size_of[Self.dtype]()
        comptime frag_size_b32 = (Self.frag_size * dst_size) // 4
        # 16 x 256b results in repeated 8x4<1xN> pattern, where
        comptime N = 32 // (4 * src_size)
        comptime bytes = 4 * dst_size * N
        # For fp8, the tcgen05.mma.kind::f8f6f4 reader expects K laid
        # out in 8-col groups (MMA_K=32 fp8 = 8 32-bit cols = 256 bits),
        # so use bits=256 with repeat=4 for frag_size_b32=16. bf16 keeps
        # the natural bits=128 (2 cols/repeat, 16 repeats).
        comptime bits = 256 if Self.dtype.is_float8() else 8 * bytes
        # e.g., N = 2 for fp32
        #
        # each repetition thus loads 8 columns
        # and loads 4 values per thread.
        # width == (repeat * bits * datapaths) // (32 * 32)
        comptime repeat = 64 * frag_size_b32 // bits
        # We need to reshape into a row of frags
        comptime assert (
            Self.num_m_mmas * Self.num_n_mmas * Self.frag_size
            == src_layout.size() * src_element_layout.size()
        )
        var frags = LayoutTensor[
            src_type,
            Layout(
                IntTuple(Self.num_m_mmas * Self.num_n_mmas),
                IntTuple(Self.frag_size),
            ),
            MutAnyOrigin,
            address_space=.LOCAL,
            element_layout=Layout.row_major(Self.frag_size),
        ](src.ptr)
        # frags = src.vectorize[1, Self.frag_size]()
        # assume src loaded with 256 bits
        comptime assert src_size >= dst_size
        comptime assert Self.num_m_mmas == 1
        comptime assert Self.num_n_mmas == 1

        comptime for m_mma in range(Self.num_m_mmas):
            var tmem = self.offset[m_mma, 0]()
            var frag = bitcast[.uint32, frag_size_b32](
                frags[m_mma].cast[Self.dtype]()
            )
            # 16 x 256b results in repeated 8x4<1x64b> pattern
            # 256b means 256 // 4 = 64b per thread
            var frag_st2 = Array[UInt32, frag_size_b32](uninitialized=True)

            comptime if Self.dtype.is_float8():
                # The SS-D fragment per thread (output of Q@K^T MMA) puts
                # each 4-lane group at a 2x2 (M, N) block. For fp8 with
                # 1 u32 = 4 fp8, the TS MMA (kind::f8f6f4) reader expects
                # 4 K-consecutive fp8 in ONE M-row per u32. The two layouts
                # disagree at thread-granularity: the data we need also
                # lives in OTHER threads' registers. Redistribute via
                # warp shuffles.
                # 1. Each thread iterates s_dst over its 16 destination u32 slots.
                # 2. For each slot, identifies which (M, K) quartet it owns in TS A layout via mma_n_tile, k_lo_half, m_local_src.
                # 3. Uses warp.shuffle_idx to pull u32 values from two peer lanes in the same lane_row (the SS-D layout puts the K-cells we want on different lane_cols of the same row).
                # 4. Picks the m_local_src-th u16 half of each of the two received u32s and concatenates them as the destination u32 (the SIMD half-word concat we just refactored to).

                # Warp grid is 8 lane_rows x 4 lane_cols (32 lanes total).
                comptime lane_cols_per_row = 4
                # SS-D M-pair: each thread owns 2 M positions
                # {lane_row, lane_row + 8} in its own registers.
                comptime m_per_pair = 2
                # TS A packs a K-quartet (4 fp8) per u32. We assemble it
                # from a low half (K, K+1) and a high half (K+2, K+3),
                # each sourced from a distinct peer lane.
                comptime k_halves_per_n_tile = 2
                comptime lane_cols_per_k_half = (
                    lane_cols_per_row // k_halves_per_n_tile  # = 2
                )
                # 4 destination u32 slots per N-tile = the (M, K-half)
                # outer product across the M-pair and the two K-halves.
                comptime dst_slots_per_n_tile = (
                    m_per_pair * k_halves_per_n_tile
                )
                # Source SS-D layout also packs 4 u32 slots per N-tile:
                # the 4 lane_cols of one lane_row each contribute one slot.
                comptime src_slots_per_n_tile = lane_cols_per_row

                var lane_row_ui = UInt32(lane_id()) // lane_cols_per_row
                var lane_col_ui = UInt32(lane_id()) % lane_cols_per_row

                comptime for s_dst in range(frag_size_b32):
                    comptime mma_n_tile = s_dst // dst_slots_per_n_tile
                    comptime k_lo_half = s_dst % k_halves_per_n_tile
                    comptime m_local_src = (
                        s_dst // k_halves_per_n_tile
                    ) % m_per_pair

                    var l_src = lane_row_ui
                    var src_lane_a = l_src * lane_cols_per_row + UInt32(
                        k_lo_half * lane_cols_per_k_half
                    )
                    var src_lane_b = src_lane_a + 1
                    var received_a: UInt32 = 0
                    var received_b: UInt32 = 0
                    # Each lane_col publishes a different slot of a_frag;
                    # only the iteration matching this thread's lane_col
                    # contributes to its output u32.
                    comptime for c_val in range(src_slots_per_n_tile):
                        comptime publisher_slot = (
                            mma_n_tile * src_slots_per_n_tile + c_val
                        )
                        var val: UInt32 = frag[publisher_slot]
                        var ra = warp.shuffle_idx(val, src_lane_a)
                        var rb = warp.shuffle_idx(val, src_lane_b)
                        if lane_col_ui == UInt32(c_val):
                            received_a = ra
                            received_b = rb

                    comptime which_half = Int(m_local_src)
                    var ab_halves = bitcast[.uint16, 4](
                        SIMD[.uint32, 2](received_a, received_b)
                    )
                    # ab_halves = [a_lo, a_hi, b_lo, b_hi]
                    var packed = SIMD[.uint16, 2](
                        ab_halves[which_half],
                        ab_halves[which_half + 2],
                    )
                    frag_st2[s_dst] = bitcast[.uint32, 1](packed)
            else:
                comptime for _i in range(frag_size_b32):
                    frag_st2[_i] = frag[_i]

            tcgen05_st[
                datapaths=16,  # first dimension of the shape
                bits=bits,  # second dimension of the shape
                repeat=repeat,
                pack=False,
            ](tmem, frag_st2)
        tcgen05_store_wait()
        named_barrier[Int32(Self.num_softmax_threads)]()

    @always_inline
    def copy_to[
        dst_type: DType,
        dst_layout: Layout,
        dst_element_layout: Layout,
        //,
    ](
        self,
        dst: LayoutTensor[
            dst_type,
            dst_layout,
            MutAnyOrigin,
            address_space=.LOCAL,
            element_layout=dst_element_layout,
        ],
    ):
        # src has row of frags layout
        comptime num_frags = dst_layout[0].size()
        comptime assert num_frags == Self.num_m_mmas * Self.num_n_mmas
        comptime assert Self.frag_size == dst_layout[1].size()
        comptime assert dst_element_layout.size() == 1
        comptime assert size_of[dst_type]() == 4
        # 16 x 256b results in repeated 8x4<1x2> pattern
        # each repetition thus loads 8 columns
        # and loads 4 values per thread.
        comptime src_size = size_of[Self.dtype]()
        comptime dst_size = size_of[dst_type]()
        comptime frag_size_b32 = (Self.frag_size * src_size) // 4
        # 16 x 256b results in repeated 8x4<1xN> pattern, where
        comptime N = 32 // (4 * dst_size)
        comptime bytes = 4 * src_size * N
        comptime bits = 8 * bytes
        # e.g., N = 2 for fp32
        #
        # each repetition thus loads 8 columns
        # and loads 4 values per thread.
        # width == (repeat * bits * datapaths) // (32 * 32)
        comptime repeat = 64 * frag_size_b32 // bits
        #
        var frags = dst.vectorize[1, Self.frag_size]()
        # assume src loaded with 256 bits
        comptime assert src_size <= dst_size
        comptime assert Self.num_n_mmas == 1

        comptime for m_mma in range(Self.num_m_mmas):
            var tmem = self.offset[m_mma, 0]()
            # 16 x 256b results in repeated 8x4<1x2> pattern
            var _ld_result2 = tcgen05_ld[
                datapaths=16,  # first dimension of the shape
                bits=bits,  # second dimension of the shape
                repeat=repeat,
                dtype=DType.uint32,
                pack=False,
                width=frag_size_b32,
            ](tmem)
            var _ld_simd2 = SIMD[.uint32, frag_size_b32]()

            comptime for _i in range(frag_size_b32):
                _ld_simd2[_i] = _ld_result2[_i]
            frags[m_mma, 0] = rebind[
                SIMD[dst_type, type_of(frags).element_size]
            ](bitcast[Self.dtype, Self.frag_size](_ld_simd2).cast[dst_type]())
        tcgen05_load_wait()


struct UMMADescriptorTS[
    operand_type: DType,
    num_m_mmas: Int,
    num_n_mmas: Int,
    *,
    MMA_M: Int,
    MMA_N: Int,
    MMA_K: Int,
    consumer_group_size: Int,
](DescriptorPairTS, TrivialRegisterPassable):
    """
    Pairs a TMEM A-operand descriptor with a shared-memory B-operand descriptor for a tensor-shared (TS) UMMA.

    Parameters:
        operand_type: The element type of the A and B operands.
        num_m_mmas: Number of UMMA tiles along the M dimension.
        num_n_mmas: Number of UMMA tiles along the N dimension.
        MMA_M: The M dimension of a single UMMA instruction tile.
        MMA_N: The N dimension of a single UMMA instruction tile.
        MMA_K: The K dimension of a single UMMA instruction tile.
        consumer_group_size: Number of threads in the consumer warp group.
    """

    comptime operand_t = Self.operand_type
    comptime a_t = TMemOperand[
        Self.operand_type,
        Self.num_m_mmas,
        Self.num_n_mmas,
        Self.MMA_M,
        Self.MMA_N,
        Self.MMA_K,
        Self.consumer_group_size,
    ]
    comptime b_t = MMASmemDescriptor

    var a: Self.a_t
    var b: Self.b_t

    @always_inline
    def __init__(out self, a: Self.a_t, b: Self.b_t):
        self.a = a
        self.b = b

    @always_inline
    def get_a(self) -> Self.a_t:
        return self.a

    @always_inline
    def get_b(self) -> Self.b_t:
        return self.b


struct SM100TensorAccumulatorSS[
    operand_type: DType,
    accum_type: DType,
    MMA_M: Int,
    MMA_N: Int,
    BM: Int,
    BN: Int,
    BK: Int,
    compute_BK: Int,
    num_softmax_threads: Int,
    swizzle_a: TensorMapSwizzle = TensorMapSwizzle.SWIZZLE_128B,
    swizzle_b: TensorMapSwizzle = TensorMapSwizzle.SWIZZLE_128B,
    *,
    transpose_b: Bool = True,
    cta_group: Int = 1,
    pipeline_stages: Int = 1,
](TrivialRegisterPassable):
    """
    Manages a shared-shared (SS) UMMA accumulator pipeline for SM100, coordinating MMA, TMEM, and barrier synchronization between producer and consumer warps.

    Parameters:
        operand_type: The element type of the A and B operands.
        accum_type: The element type of the accumulator.
        MMA_M: The M dimension of a single UMMA instruction tile.
        MMA_N: The N dimension of a single UMMA instruction tile.
        BM: The M dimension of the accumulator block tile in elements.
        BN: The N dimension of the accumulator block tile in elements.
        BK: The K dimension of the operand block tile in elements.
        compute_BK: The K dimension used for the compute loop in elements.
        num_softmax_threads: Number of threads in the softmax consumer warp group.
        swizzle_a: The shared-memory swizzle mode for the A operand
            (defaults to `SWIZZLE_128B`).
        swizzle_b: The shared-memory swizzle mode for the B operand
            (defaults to `SWIZZLE_128B`).
        transpose_b: Whether the B operand is stored transposed
            (defaults to `True`).
        cta_group: The CTA group index used to dispatch the MMA (defaults to 1).
        pipeline_stages: Number of double-buffered pipeline stages
            (defaults to 1).
    """

    comptime operand_t: DType = Self.operand_type
    comptime accum_t: DType = Self.accum_type

    comptime MMA_K = 16 if Self.operand_t.is_half_float() else 32
    comptime mma_kind = (
        UMMAKind.KIND_F8F6F4 if Self.operand_t.is_float8() else UMMAKind.KIND_F16
    )

    comptime num_m_mmas = Self.BM // Self.MMA_M
    comptime num_n_mmas = Self.BN // Self.MMA_N
    comptime num_k_mmas = Self.compute_BK // Self.MMA_K

    comptime num_m_blocks_per_warp = 2 * Self.BM // Self.num_softmax_threads

    comptime smem_ptr_t = UnsafePointer[
        Scalar[Self.operand_t], MutAnyOrigin, address_space=.SHARED
    ]

    comptime a_offset = MMAOperandOffsetFn[
        Self.operand_t,
        Self.BM,
        Self.BK,
        Self.swizzle_a,
        True,
        Self.MMA_M,
        Self.MMA_K,
    ]()
    comptime b_offset = MMAOperandOffsetFn[
        Self.operand_t,
        Self.BN,
        Self.BK,
        Self.swizzle_b,
        Self.transpose_b,
        Self.MMA_N,
        Self.MMA_K,
    ]()

    comptime idesc = UMMAInsDescriptor[Self.mma_kind].create[
        Self.accum_t,
        Self.operand_t,
        Self.operand_t,
        Index[dtype=DType.uint32](Self.MMA_M, Self.MMA_N),
        transpose_b=Self.transpose_b,
    ]()

    comptime ab_t: DescriptorPair = UMMADescriptorSS[Self.operand_t]
    comptime a_t: MMAOperandDescriptor = Self.ab_t.a_t
    comptime b_t: MMAOperandDescriptor = Self.ab_t.b_t
    comptime c_t: AccumulatorTile = TMemAccumulator[
        Self.accum_t,
        Self.BM // Self.num_m_blocks_per_warp,
        Self.MMA_N,
        Self.num_m_blocks_per_warp,
        Self.num_n_mmas,
        Self.num_softmax_threads,
    ]

    @__allow_legacy_any_origin_fields
    var mbar: UnsafePointer[
        SharedMemBarrier, MutAnyOrigin, address_space=.SHARED
    ]
    var pipeline: PipelineState[Self.pipeline_stages]

    @always_inline
    @staticmethod
    def check_constraints():
        comptime assert (Self.BM % Self.MMA_M) == 0, (
            "BM, MMA_M = " + String(Self.BM) + ", " + String(Self.MMA_M)
        )
        comptime assert ((Self.BN % Self.MMA_N) == 0) and (
            Self.num_n_mmas > 0
        ), ("BN, MMA_N = " + String(Self.BN) + ", " + String(Self.MMA_N))
        comptime assert ((Self.compute_BK % Self.MMA_K) == 0) and (
            Self.num_k_mmas > 0
        ), (
            "compute_BK, MMA_K = "
            + String(Self.compute_BK)
            + ", "
            + String(Self.MMA_K)
        )

    @always_inline
    def __init__(
        out self,
        smem: UnsafePointer[
            SharedMemBarrier, MutAnyOrigin, address_space=.SHARED
        ],
    ):
        Self.check_constraints()
        self.mbar = smem
        self.pipeline = {}

    @always_inline
    def init(self):
        comptime for i in range(Self.pipeline_stages):
            self.mbar[i].init()
            self.mbar[i + Self.pipeline_stages].init(
                Int32(Self.num_softmax_threads)
            )

    @staticmethod
    @always_inline
    def mma_descriptors[
        dtype_a: DType, dtype_b: DType
    ](
        p_a: UnsafePointer[
            Scalar[dtype_a], MutAnyOrigin, address_space=.SHARED
        ],
        p_b: UnsafePointer[
            Scalar[dtype_b], MutAnyOrigin, address_space=.SHARED
        ],
    ) -> Self.ab_t:
        Self.check_constraints()
        comptime a_canonical_layout = Self.a_offset.canonical_layout
        comptime a_type = Self.operand_t
        comptime aSBO = a_canonical_layout[0].stride[1].value() * size_of[
            a_type
        ]()
        comptime aLBO = a_canonical_layout[1].stride[1].value() * size_of[
            a_type
        ]()
        var adesc_base = MMASmemDescriptor.create[aSBO, aLBO, Self.swizzle_a](
            p_a
        )

        comptime b_canonical_layout = Self.b_offset.canonical_layout
        comptime b_type = Self.operand_t
        comptime b_stride01 = b_canonical_layout[0].stride[1].value()
        comptime b_stride11 = b_canonical_layout[1].stride[1].value()
        comptime bSBO = (
            b_stride01 if Self.transpose_b else b_stride11
        ) * size_of[b_type]()
        comptime bLBO = (
            b_stride11 if Self.transpose_b else b_stride01
        ) * size_of[b_type]()
        var bdesc_base = MMASmemDescriptor.create[bSBO, bLBO, Self.swizzle_b](
            p_b
        )

        return Self.ab_t(adesc_base, bdesc_base)

    @always_inline
    def mma(
        mut self,
        a: Self.a_t,
        b: Self.b_t,
        c_base: Self.c_t,
        scale_c: UInt32,
    ):
        var c = c_base[self.pipeline.index()]

        comptime for n_mma in range(Self.num_n_mmas):
            comptime for m_mma in range(Self.num_m_mmas):
                var c_tmem = c.offset[m_mma, n_mma]()
                comptime for k_mma in range(Self.num_k_mmas):
                    comptime a_offset = Self.a_offset.layout(
                        IntTuple(Self.MMA_M * m_mma, Self.MMA_K * k_mma)
                    )
                    comptime a_offset_bytes = a_offset * size_of[
                        Self.operand_t
                    ]()
                    var a_desc = a + a_offset_bytes

                    comptime b_offset = Self.b_offset.layout(
                        IntTuple(Self.MMA_N * n_mma, Self.MMA_K * k_mma)
                    ) * size_of[Self.operand_t]()
                    var b_desc = b + b_offset

                    comptime if k_mma == 0:
                        mma[Self.cta_group](
                            a_desc,
                            b_desc,
                            c_tmem,
                            Self.idesc,
                            scale_c,
                        )
                    else:
                        mma[Self.cta_group, c_scale=1](
                            a_desc, b_desc, c_tmem, Self.idesc
                        )

        mma_arrive(self.mbar + self.pipeline.index())
        self.pipeline.step()

    # the mma thread
    # loop:
    #   wait_for_tmem() # self.mbar[Stages + index()].wait(phase())
    #   mma()           # self.mbar[index()].arrive(), step()
    #
    # the softmax thread
    #
    # tmem_arrive_init() # for i in range(Stages): self.mbar[Stages + i].arrive()
    #
    # loop:
    #   wait_for_mma()   # self.mbar[index()].wait(phase())
    #   use accumulator
    #   tmem_arrive()    # self.mbar[Stages + index()].arrive(), step()
    @always_inline
    def wait_for_tmem(self):
        """
        Wait for the accumulator tmem to finish being read.
        """
        self.mbar[UInt32(Self.pipeline_stages) + self.pipeline.index()].wait(
            self.pipeline.phase()
        )

    @always_inline
    def wait_for_mma(self, c_base: Self.c_t) -> Self.c_t:
        """
        Wait for the accumulator tmem to finish being read.

        Args:
            c_base: The accumulator tile base indexed by pipeline stage.
        """
        var idx: UInt32 = self.pipeline.index()
        self.mbar[idx].wait(self.pipeline.phase())
        return c_base[idx]

    @always_inline
    def tmem_arrive_init(self):
        comptime for i in range(Self.pipeline_stages):
            _ = self.mbar[Self.pipeline_stages + i].arrive()

    @always_inline
    def tmem_arrive(mut self):
        """
        Indicate that the accumulator is ready to be updated.
        """
        _ = self.mbar[
            UInt32(Self.pipeline_stages) + self.pipeline.index()
        ].arrive()
        self.pipeline.step()


struct SM100TensorAccumulatorTS[
    operand_type: DType,
    accum_type: DType,
    MMA_M: Int,
    MMA_N: Int,
    BM: Int,
    BN: Int,
    BK: Int,
    num_softmax_threads: Int,
    swizzle_b: TensorMapSwizzle = TensorMapSwizzle.SWIZZLE_128B,
    transpose_b: Bool = True,
    cta_group: Int = 1,
](TrivialRegisterPassable):
    """
    Manages a tensor-shared (TS) UMMA accumulator pipeline for SM100, coordinating MMA between a TMEM A-operand and a shared-memory B-operand.

    Parameters:
        operand_type: The element type of the A and B operands.
        accum_type: The element type of the accumulator.
        MMA_M: The M dimension of a single UMMA instruction tile.
        MMA_N: The N dimension of a single UMMA instruction tile.
        BM: The M dimension of the accumulator block tile in elements.
        BN: The N dimension of the accumulator block tile in elements.
        BK: The K dimension of the operand block tile in elements.
        num_softmax_threads: Number of threads in the softmax consumer warp
            group.
        swizzle_b: The shared-memory swizzle mode for the B operand
            (defaults to `SWIZZLE_128B`).
        transpose_b: Whether the B operand is stored transposed
            (defaults to `True`).
        cta_group: The CTA group index used to dispatch the MMA
            (defaults to 1).
    """

    comptime operand_t: DType = Self.operand_type
    comptime accum_t: DType = Self.accum_type

    comptime MMA_K = 16 if Self.operand_t.is_half_float() else 32
    comptime mma_kind = (
        UMMAKind.KIND_F8F6F4 if Self.operand_t.is_float8() else UMMAKind.KIND_F16
    )
    comptime smem_ptr_t = UnsafePointer[
        Scalar[Self.operand_t], MutAnyOrigin, address_space=.SHARED
    ]

    comptime num_m_mmas = Self.BM // Self.MMA_M
    comptime num_n_mmas = Self.BN // Self.MMA_N
    comptime num_k_mmas = Self.BK // Self.MMA_K
    comptime c_frag_size = Self.MMA_M * Self.MMA_N // Self.num_softmax_threads
    comptime a_frag_size = Self.MMA_M * Self.MMA_K // Self.num_softmax_threads
    comptime num_m_blocks_per_warp = 2 * Self.BM // Self.num_softmax_threads
    comptime ab_t: DescriptorPairTS = UMMADescriptorTS[
        Self.operand_t,
        Self.num_m_blocks_per_warp,
        Self.num_n_mmas,
        MMA_M=Self.BM // Self.num_m_blocks_per_warp,
        MMA_N=Self.BK,
        MMA_K=Self.MMA_K,
        consumer_group_size=Self.num_softmax_threads,
    ]
    comptime a_t: WriteableMMAOperandDescriptor = Self.ab_t.a_t
    comptime b_t: MMAOperandDescriptor = Self.ab_t.b_t

    comptime b_offset = MMAOperandOffsetFn[
        Self.operand_t,
        Self.BN,
        Self.BK,
        Self.swizzle_b,
        Self.transpose_b,
        Self.MMA_N,
        Self.MMA_K,
    ]()
    comptime c_t: AccumulatorTile = TMemAccumulator[
        Self.accum_t,
        Self.BM // Self.num_m_blocks_per_warp,
        Self.MMA_N,
        Self.num_m_blocks_per_warp,
        Self.num_n_mmas,
        Self.num_softmax_threads,
    ]

    comptime idesc = UMMAInsDescriptor[Self.mma_kind].create[
        Self.accum_t,
        Self.operand_t,
        Self.operand_t,
        Index[dtype=DType.uint32](Self.MMA_M, Self.MMA_N),
        transpose_b=Self.transpose_b,
    ]()

    @__allow_legacy_any_origin_fields
    var mbar: UnsafePointer[
        SharedMemBarrier, MutAnyOrigin, address_space=.SHARED
    ]
    var phase: UInt32

    @staticmethod
    @always_inline
    def check_constraints():
        comptime assert (Self.BM % Self.MMA_M) == 0, (
            "BM, MMA_M = " + String(Self.BM) + ", " + String(Self.MMA_M)
        )
        comptime assert ((Self.BN % Self.MMA_N) == 0) and (
            Self.num_n_mmas > 0
        ), ("BN, MMA_N = " + String(Self.BN) + ", " + String(Self.MMA_N))
        comptime assert ((Self.BK % Self.MMA_K) == 0) and (
            Self.num_k_mmas > 0
        ), ("BK, MMA_K = " + String(Self.BK) + ", " + String(Self.MMA_K))

    @always_inline
    def __init__(
        out self,
        smem: UnsafePointer[
            SharedMemBarrier, MutAnyOrigin, address_space=.SHARED
        ],
    ):
        Self.check_constraints()
        self.mbar = smem
        self.phase = 0

    @always_inline
    def init(self):
        self.mbar[0].init()
        self.mbar[1].init(Int32(Self.num_softmax_threads))

    @staticmethod
    @always_inline
    def a_mma_descriptor(a_tmem: UInt32) -> Self.ab_t.a_t:
        Self.check_constraints()
        return Self.ab_t.a_t(a_tmem)

    @staticmethod
    @always_inline
    def b_mma_descriptor[
        dtype_b: DType
    ](
        p_b: UnsafePointer[
            Scalar[dtype_b], MutAnyOrigin, address_space=.SHARED
        ],
    ) -> Self.ab_t.b_t:
        Self.check_constraints()
        comptime b_canonical_layout = Self.b_offset.canonical_layout
        comptime b_type = Self.operand_t
        comptime b_stride01 = b_canonical_layout[0].stride[1].value()
        comptime b_stride11 = b_canonical_layout[1].stride[1].value()
        comptime bSBO = (
            b_stride01 if Self.transpose_b else b_stride11
        ) * size_of[b_type]()
        comptime bLBO = (
            b_stride11 if Self.transpose_b else b_stride01
        ) * size_of[b_type]()

        return MMASmemDescriptor.create[bSBO, bLBO, Self.swizzle_b](p_b)

    @always_inline
    def mma(
        self,
        a: Self.a_t,
        b: Self.b_t,
        c: Self.c_t,
        c_scale: UInt32,
    ):
        comptime for k_mma in range(Self.num_k_mmas):
            comptime for m_mma in range(Self.num_m_mmas):
                var a_tmem = a.offset[m_mma=m_mma, k_mma=k_mma]()

                comptime for n_mma in range(Self.num_n_mmas):
                    var c_tmem = c.offset[m_mma=m_mma, n_mma=n_mma]()
                    comptime b_offset = Self.b_offset.layout(
                        IntTuple(Self.MMA_N * n_mma, Self.MMA_K * k_mma)
                    ) * size_of[Self.operand_t]()
                    var b_desc = b + b_offset

                    comptime if k_mma == 0:
                        mma[Self.cta_group](
                            a_tmem,
                            b_desc,
                            c_tmem,
                            Self.idesc,
                            c_scale,
                        )
                    else:
                        mma[Self.cta_group, c_scale=1](
                            a_tmem, b_desc, c_tmem, Self.idesc
                        )
        mma_arrive(self.mbar)

    # the mma thread
    # loop:
    #   wait_for_tmem()   # self.mbar[1].wait(self.phase), self.phase ^= 1
    #   mma()             # self.mbar[0].arrive()
    #
    # the softmax thread
    # tmem_arrive()       # self.mbar[1].arrive()
    #
    # loop:
    #   wait_for_mma()    # self.mbar[0].wait(self.phase), self.phase ^= 1
    #   scale output, write P
    #   tmem_arrive()     # self.mbar[1].arrive()
    @always_inline
    def wait(mut self, idx: UInt32):
        # update the phase before waiting
        var old_phase: UInt32 = self.phase
        self.phase = old_phase ^ 1
        self.mbar[idx].wait(old_phase)

    @always_inline
    def wait_for_mma(mut self):
        """
        Wait for the mma to be complete.
        """
        self.wait(0)

    @always_inline
    def wait_for_tmem(mut self):
        """
        Wait for the `output` and `A` tmem to be ready.
        """
        self.wait(1)

    @always_inline
    def tmem_arrive(self):
        """
        Indicate that the accumulator and the tensor memory arguments
        are ready for the MMA to begin.
        """
        _ = self.mbar[1].arrive()


@always_inline
def mha_sm100_dispatch[
    q_type: DType,
    KVType: MHAOperand,
    MaskType: MHAMask,
    output_type: DType,
    MaxPromptLenType: OptionallyStaticInt,
    PartitionType: MHAPartitionScheme,
    //,
    config: MHAConfig,
    group: Int,
    ragged: Bool,
    sink: Bool,
    _is_cache_length_accurate: Bool,
](
    output: DeviceBuffer[output_type],
    q_arg: DeviceBuffer[q_type],
    k: KVType,
    v: KVType,
    num_rows_q: Int,
    mask: MaskType,
    valid_length: DeviceBuffer[.uint32],
    max_prompt_len_arg: MaxPromptLenType,
    max_cache_valid_length_arg: Int,
    scale: Float32,
    kv_input_row_offsets: OptionalReg[ImmutTileTensor1D[.uint32]],
    batch_size_arg: Int,
    partition: PartitionType,
    ctx: DeviceContext,
    sink_weights: OptionalReg[ImmutTileTensor1D[q_type]],
) raises:
    """
    Dispatches single-query (decode) multi-head attention to the SM100 kernel, selecting TMA tiles, scheduler, and partition configuration.

    Parameters:
        q_type: The element type of the query tensor.
        KVType: The operand type describing the key and value memory
            layout (for example, paged or ragged).
        MaskType: The mask type applied to the attention scores.
        output_type: The element type of the output tensor.
        MaxPromptLenType: The type representing the maximum prompt
            length, which may be statically or dynamically known.
        PartitionType: The scheme for partitioning work across CTAs.
        config: The MHA configuration holding tile sizes, depth, and head
            count.
        group: The group-query attention group size, equal to the number
            of query heads sharing each KV head.
        ragged: Whether the input sequences have variable lengths.
        sink: Whether attention sink weights are applied.
        _is_cache_length_accurate: Whether the reported cache length is
            exact, affecting position computation.

    Args:
        output: The output buffer for the attention results.
        q_arg: The query tensor buffer.
        k: The key operand, with layout described by `KVType`.
        v: The value operand, with layout described by `KVType`.
        num_rows_q: Number of query rows in the batch.
        mask: The attention mask instance.
        valid_length: The per-sequence valid length buffer.
        max_prompt_len_arg: The maximum prompt length across the batch.
        max_cache_valid_length_arg: The maximum valid cache length.
        scale: The scaling factor applied to the query-key dot product.
        kv_input_row_offsets: Optional row offsets into the KV input.
            for ragged layouts.
        batch_size_arg: The number of sequences in the batch.
        partition: The partition descriptor for splitting work across
            CTAs.
        ctx: The device context used to launch the kernel.
        sink_weights: Optional sink weights for attention sinks.
    """
    comptime assert _is_decoding[MaxPromptLenType](), "mha_1q is decode-only"
    comptime new_config = MHAConfig[config.dtype](
        config.num_heads,
        config.depth,
        num_queries_per_block=64,
        num_keys_per_block=config.num_keys_per_block,
        BK=config.BK,
        num_pipeline_stages=2 if config.padded_depth >= 512 else 4,
    )
    comptime BM = new_config.block_m()
    comptime BK = new_config.padded_depth
    comptime assert BM % 64 == 0, "SM90 requires BM%64==0, but BM==" + String(
        BM
    )
    comptime assert (
        BK % 64 == 0
    ), "B200 requires BK%64 as it uses 128B swizzles, but BK==" + String(BK)
    comptime BN = new_config.block_n()
    # add the number of producer threads (i.e. 1 WARP_GROUP_SIZE)
    comptime num_threads = new_config.num_threads[True]()
    comptime assert num_threads % 128 == 0, "num_threads = " + String(
        num_threads
    )
    comptime assert (
        config.dtype == KVType.dtype and config.dtype == q_type
    ), "config, kv, and q types must all match for FA3."

    var q = (
        q_arg.unsafe_ptr()
        .bitcast[Scalar[KVType.dtype]]()
        .unsafe_origin_cast[MutAnyOrigin]()
    )

    # Persistent kernels not currently supported with partitioning
    # This doesn't seem useful: we partition to make SMs more busy,
    # implying we don't have enough to make them persistent.
    # This also requires some tricky control flow handling to support,
    # which we haven't added yet.
    comptime assert new_config.algorithm == FlashAttentionAlgorithm(3)

    var max_cache_valid_length: UInt32 = UInt32(max_cache_valid_length_arg)
    var batch_size: UInt32 = UInt32(batch_size_arg)

    comptime num_scheduler_heads = config.num_heads // group
    comptime scheduler_tile_shape = 1
    comptime swizzle_mode = (
        TensorMapSwizzle.SWIZZLE_64B if config.dtype.is_float8() else TensorMapSwizzle.SWIZZLE_128B
    )
    var q_tma_op = rebind[
        QTMATile[
            KVType.dtype,
            swizzle_mode,
            BM=new_config.block_m(),
            depth=new_config.depth,
            group=group,
            decoding=_is_decoding[MaxPromptLenType](),
        ]
    ](
        q_tma[
            swizzle_mode,
            BM=BM,
            depth=new_config.depth,
            q_num_heads=new_config.num_heads,
            group=group,
            decoding=_is_decoding[MaxPromptLenType](),
        ](ctx, q, num_rows_q)
    )
    comptime kv_sub_BN = _kv_sub_tile_rows(
        new_config.block_n(), KVType.page_size
    )
    var k_tma_op = k.create_tma_tile[
        swizzle_mode,
        BN=kv_sub_BN,
        depth=new_config.depth,
        BK=new_config.padded_depth,
    ](ctx)
    var v_tma_op = v.create_tma_tile[
        swizzle_mode,
        BN=kv_sub_BN,
        depth=new_config.depth,
        BK=new_config.padded_depth,
    ](ctx)

    comptime SchedulerType = TransientScheduler[
        UInt32(scheduler_tile_shape),
        UInt32(num_scheduler_heads),
        flip_prompt_idx=False,
    ]
    var scheduler: SchedulerType = SchedulerType()

    comptime if sink:
        comptime SinkType = NonNullPointer[KVType.dtype]
        var sink_ptr: SinkType = {
            rebind[UnsafePointer[Scalar[KVType.dtype], ImmutAnyOrigin]](
                sink_weights.value().ptr
            )
        }
        _mha_sm100_kv_input_row_offset_dispatch[
            SchedulerType=SchedulerType,
            KVLUTType=KVType,
            output_type=output_type,
            MaxSeqLenType=MaxPromptLenType,
            PartitionType=PartitionType,
            MaskType=MaskType,
            config=new_config,
            group=group,
            ragged=ragged,
            SinkType=SinkType,
            _is_cache_length_accurate=_is_cache_length_accurate,
            swizzle_mode=swizzle_mode,
        ](
            scheduler,
            q_tma_op,
            k_tma_op,
            v_tma_op,
            output,
            k,
            scale,
            batch_size,
            max_prompt_len_arg,
            max_cache_valid_length,
            valid_length,
            kv_input_row_offsets,
            sink_ptr,
            partition,
            mask,
            ctx,
        )
    else:
        comptime SinkType = NullPointer[KVType.dtype]
        comptime sink_ptr: SinkType = {}
        _mha_sm100_kv_input_row_offset_dispatch[
            SchedulerType=SchedulerType,
            KVLUTType=KVType,
            output_type=output_type,
            MaxSeqLenType=MaxPromptLenType,
            PartitionType=PartitionType,
            MaskType=MaskType,
            config=new_config,
            group=group,
            ragged=ragged,
            SinkType=SinkType,
            _is_cache_length_accurate=_is_cache_length_accurate,
            swizzle_mode=swizzle_mode,
        ](
            scheduler,
            q_tma_op,
            k_tma_op,
            v_tma_op,
            output,
            k,
            scale,
            batch_size,
            max_prompt_len_arg,
            max_cache_valid_length,
            valid_length,
            kv_input_row_offsets,
            sink_ptr,
            partition,
            mask,
            ctx,
        )


@always_inline
def _mha_sm100_kv_input_row_offset_dispatch[
    KVLUTType: MHAOperand,
    output_type: DType,
    MaskType: MHAMask,
    SchedulerType: MHATileScheduler,
    config: MHAConfig,
    group: Int,
    ragged: Bool,
    SinkType: OptionalPointer,
    _is_cache_length_accurate: Bool,
    MaxSeqLenType: OptionallyStaticInt,
    PartitionType: MHAPartitionScheme,
    swizzle_mode: TensorMapSwizzle,
](
    scheduler: SchedulerType,
    q_tma_op: QTMATile[
        KVLUTType.dtype,
        swizzle_mode,
        BM=config.block_m(),
        depth=config.depth,
        group=group,
        decoding=_is_decoding[MaxSeqLenType](),
    ],
    k_tma_op: KVTMATile[
        KVLUTType.dtype,
        swizzle_mode,
        BN=_kv_sub_tile_rows(config.block_n(), KVLUTType.page_size),
        BK=config.padded_depth,
    ],
    v_tma_op: KVTMATile[
        KVLUTType.dtype,
        swizzle_mode,
        BN=_kv_sub_tile_rows(config.block_n(), KVLUTType.page_size),
        BK=config.padded_depth,
    ],
    o_ptr_arg: DeviceBuffer[output_type],
    kv_lut: KVLUTType,
    scale: Float32,
    batch_size: UInt32,
    max_seq_len: MaxSeqLenType,  # sequence length after padding.
    num_keys_arg: UInt32,
    valid_length: DeviceBuffer[.uint32],
    kv_input_row_offsets: OptionalReg[ImmutTileTensor1D[.uint32]],
    sink_weights: SinkType,
    partition: PartitionType,
    mask: MaskType,
    ctx: DeviceContext,
) raises:
    comptime KVRowOffsetsNonNull = NonNullPointer[.uint32]
    comptime KVRowOffsetsNull = NullPointer[.uint32]
    if kv_input_row_offsets:
        var kv_row_offsets: KVRowOffsetsNonNull = {
            kv_input_row_offsets.value().ptr
        }
        _mha_sm100_valid_length_dispatch[
            SchedulerType=SchedulerType,
            KVLUTType=KVLUTType,
            output_type=output_type,
            MaxSeqLenType=MaxSeqLenType,
            PartitionType=PartitionType,
            MaskType=MaskType,
            config=config,
            group=group,
            ragged=ragged,
            SinkType=SinkType,
            KVRowOffsetsType=KVRowOffsetsNonNull,
            _is_cache_length_accurate=_is_cache_length_accurate,
            swizzle_mode=swizzle_mode,
        ](
            scheduler,
            q_tma_op,
            k_tma_op,
            v_tma_op,
            o_ptr_arg,
            kv_lut,
            scale,
            batch_size,
            max_seq_len,
            num_keys_arg,
            valid_length,
            kv_row_offsets,
            sink_weights,
            partition,
            mask,
            ctx,
        )
    else:
        var kv_row_offsets: KVRowOffsetsNull = {}
        _mha_sm100_valid_length_dispatch[
            SchedulerType=SchedulerType,
            KVLUTType=KVLUTType,
            output_type=output_type,
            MaxSeqLenType=MaxSeqLenType,
            PartitionType=PartitionType,
            MaskType=MaskType,
            config=config,
            group=group,
            ragged=ragged,
            SinkType=SinkType,
            KVRowOffsetsType=KVRowOffsetsNull,
            _is_cache_length_accurate=_is_cache_length_accurate,
            swizzle_mode=swizzle_mode,
        ](
            scheduler,
            q_tma_op,
            k_tma_op,
            v_tma_op,
            o_ptr_arg,
            kv_lut,
            scale,
            batch_size,
            max_seq_len,
            num_keys_arg,
            valid_length,
            kv_row_offsets,
            sink_weights,
            partition,
            mask,
            ctx,
        )


@always_inline
def _mha_sm100_valid_length_dispatch[
    KVLUTType: MHAOperand,
    output_type: DType,
    MaskType: MHAMask,
    SchedulerType: MHATileScheduler,
    config: MHAConfig,
    group: Int,
    ragged: Bool,
    SinkType: OptionalPointer,
    KVRowOffsetsType: OptionalPointer,
    _is_cache_length_accurate: Bool,
    MaxSeqLenType: OptionallyStaticInt,
    PartitionType: MHAPartitionScheme,
    swizzle_mode: TensorMapSwizzle,
](
    scheduler: SchedulerType,
    q_tma_op: QTMATile[
        KVLUTType.dtype,
        swizzle_mode,
        BM=config.block_m(),
        depth=config.depth,
        group=group,
        decoding=_is_decoding[MaxSeqLenType](),
    ],
    k_tma_op: KVTMATile[
        KVLUTType.dtype,
        swizzle_mode,
        BN=_kv_sub_tile_rows(config.block_n(), KVLUTType.page_size),
        BK=config.padded_depth,
    ],
    v_tma_op: KVTMATile[
        KVLUTType.dtype,
        swizzle_mode,
        BN=_kv_sub_tile_rows(config.block_n(), KVLUTType.page_size),
        BK=config.padded_depth,
    ],
    o_ptr_arg: DeviceBuffer[output_type],
    kv_lut: KVLUTType,
    scale: Float32,
    batch_size: UInt32,
    max_seq_len: MaxSeqLenType,  # sequence length after padding.
    num_keys_arg: UInt32,
    valid_length: DeviceBuffer[.uint32],
    kv_input_row_offsets: KVRowOffsetsType,
    sink_weights: SinkType,
    partition: PartitionType,
    mask: MaskType,
    ctx: DeviceContext,
) raises:
    comptime if ragged:
        comptime ValidLengthType = NonNullPointer[.uint32]
        var valid_len: ValidLengthType = {valid_length}
        _mha_sm100_enqueue[
            SchedulerType=SchedulerType,
            KVLUTType=KVLUTType,
            output_type=output_type,
            MaxSeqLenType=MaxSeqLenType,
            PartitionType=PartitionType,
            MaskType=MaskType,
            config=config,
            group=group,
            SinkType=SinkType,
            ValidLengthType=ValidLengthType,
            KVRowOffsetsType=KVRowOffsetsType,
            _is_cache_length_accurate=_is_cache_length_accurate,
            swizzle_mode=swizzle_mode,
        ](
            scheduler,
            q_tma_op,
            k_tma_op,
            v_tma_op,
            o_ptr_arg,
            kv_lut,
            scale,
            batch_size,
            max_seq_len,
            num_keys_arg,
            valid_len,
            kv_input_row_offsets,
            sink_weights,
            partition,
            mask,
            ctx,
        )
    else:
        comptime ValidLengthType = NullPointer[.uint32]
        var valid_len: ValidLengthType = {}
        _mha_sm100_enqueue[
            SchedulerType=SchedulerType,
            KVLUTType=KVLUTType,
            output_type=output_type,
            MaxSeqLenType=MaxSeqLenType,
            PartitionType=PartitionType,
            MaskType=MaskType,
            config=config,
            group=group,
            SinkType=SinkType,
            ValidLengthType=ValidLengthType,
            KVRowOffsetsType=KVRowOffsetsType,
            _is_cache_length_accurate=_is_cache_length_accurate,
            swizzle_mode=swizzle_mode,
        ](
            scheduler,
            q_tma_op,
            k_tma_op,
            v_tma_op,
            o_ptr_arg,
            kv_lut,
            scale,
            batch_size,
            max_seq_len,
            num_keys_arg,
            valid_len,
            kv_input_row_offsets,
            sink_weights,
            partition,
            mask,
            ctx,
        )


@always_inline
def _mha_sm100_enqueue[
    KVLUTType: MHAOperand,
    output_type: DType,
    MaskType: MHAMask,
    SchedulerType: MHATileScheduler,
    config: MHAConfig,
    group: Int,
    ValidLengthType: OptionalPointer,
    SinkType: OptionalPointer,
    KVRowOffsetsType: OptionalPointer,
    _is_cache_length_accurate: Bool,
    MaxSeqLenType: OptionallyStaticInt,
    PartitionType: MHAPartitionScheme,
    swizzle_mode: TensorMapSwizzle,
](
    scheduler: SchedulerType,
    q_tma_op: QTMATile[
        KVLUTType.dtype,
        swizzle_mode,
        BM=config.block_m(),
        depth=config.depth,
        group=group,
        decoding=_is_decoding[MaxSeqLenType](),
    ],
    k_tma_op: KVTMATile[
        KVLUTType.dtype,
        swizzle_mode,
        BN=_kv_sub_tile_rows(config.block_n(), KVLUTType.page_size),
        BK=config.padded_depth,
    ],
    v_tma_op: KVTMATile[
        KVLUTType.dtype,
        swizzle_mode,
        BN=_kv_sub_tile_rows(config.block_n(), KVLUTType.page_size),
        BK=config.padded_depth,
    ],
    o_ptr_arg: DeviceBuffer[output_type],
    kv_lut: KVLUTType,
    scale: Float32,
    batch_size: UInt32,
    max_seq_len: MaxSeqLenType,  # sequence length after padding.
    num_keys_arg: UInt32,
    valid_length: ValidLengthType,  # OptionalPointer[DType.uint32]
    kv_input_row_offsets: KVRowOffsetsType,  # OptionalPointer[DType.uint32],
    sink_weights: SinkType,
    partition: PartitionType,
    mask: MaskType,
    ctx: DeviceContext,
) raises:
    # the pack contains all possibly 0-sized objects
    comptime kernel_sm100 = _mha_sm100[
        KVLUTType,
        output_type,
        MaskType,
        SchedulerType,
        config,
        group,
        ValidLengthType,
        SinkType,
        KVRowOffsetsType,
        _is_cache_length_accurate,
        MaxSeqLenType,
        PartitionType,
        swizzle_mode,
    ]
    comptime PackType = Pack[
        MaskType,
        SchedulerType,
        ValidLengthType,
        SinkType,
        KVRowOffsetsType,
        MaxSeqLenType,
        PartitionType,
    ]
    var pack: PackType = {
        mask,
        scheduler,
        valid_length,
        sink_weights,
        kv_input_row_offsets,
        max_seq_len,
        partition,
    }

    # Launch the num_keys-independent upper bound so the grid shape is stable
    # across num_keys (one CUDA graph per batch size). CTAs with partition
    # index >= partition.num_partitions() early-return in the kernel.
    var block_x: UInt32 = partition.max_num_partitions()

    comptime max_tmem_cols = 512
    comptime BN = config.block_n()
    comptime BM_enq = config.block_m()
    # When P must go to SMEM (depth too large for P+O in one TMEM bank),
    # S and O go in separate TMEM banks, giving full 512 cols for S.
    comptime use_p_smem = config.padded_depth + BN // 2 > max_tmem_cols
    comptime num_s = (
        max_tmem_cols
        // BN if use_p_smem else (
            max_tmem_cols - (BN // 2) - config.padded_depth
        )
        // BN
    )
    # P buffer in SMEM for depth=512 decoding (BM * BN * sizeof(bf16))
    comptime p_smem_bytes = BM_enq * BN * size_of[
        config.dtype
    ]() if use_p_smem else 0
    # The q_smem region is repurposed as the output tile in `write_output`
    # (bitcast to output_type), so it must hold the wider of the two roles.
    # We require output_type to be at least as wide as the KV/Q element
    # type so the byte delta below is non-negative; matched widths give 0.
    # The kernel-side `q_smem_size` mirrors this allocation.
    comptime assert size_of[output_type]() >= size_of[config.dtype](), (
        "q_smem is bitcast to output_type in write_output; output_type"
        " must be at least as wide as the KV/Q element type"
    )
    comptime q_extra_for_output_bytes = BM_enq * config.padded_depth * (
        size_of[output_type]() - size_of[config.dtype]()
    )
    # we add smem use for SharedMemBarrier synchronization
    # 2*8 for mma mbars
    comptime extra_B200_smem = (
        (2 * num_s + 3) * 8 + p_smem_bytes + q_extra_for_output_bytes
    )
    comptime smem_use = config.shared_mem_bytes[
        True, sm_90=True
    ]() + extra_B200_smem
    comptime num_threads = config.num_threads[True]()
    logger.info("------ Dispatching to SM100 FMHA-1Q ------")
    logger.info(
        "QKV Type: ",
        KVLUTType.dtype,
        "Depth:",
        config.depth,
        "Number of Q // KV Heads:",
        config.num_heads,
        "//",
        config.num_heads // group,
        "Batch Size:",
        batch_size,
        "Num Partitions:",
        partition.num_partitions(),
    )
    ctx.enqueue_function[kernel_sm100](
        q_tma_op,
        k_tma_op,
        v_tma_op,
        o_ptr_arg,
        kv_lut,
        scale,
        batch_size,
        num_keys_arg,
        pack,
        grid_dim=SchedulerType.grid_dim(batch_size, block_x),
        block_dim=(num_threads, 1, 1),
        shared_mem_bytes=smem_use,
        func_attribute=FuncAttribute.MAX_DYNAMIC_SHARED_SIZE_BYTES(
            UInt32(smem_use)
        ),
        attributes=pdl_launch_attributes(MHA_PDL_LEVEL),
    )


@__llvm_arg_metadata(q_tma_op, `nvvm.grid_constant`)
@__llvm_arg_metadata(k_tma_op, `nvvm.grid_constant`)
@__llvm_arg_metadata(v_tma_op, `nvvm.grid_constant`)
@__llvm_metadata(
    MAX_THREADS_PER_BLOCK_METADATA=StaticTuple[Int32, 1](
        Int32(config.num_threads[True]())
    )
)
@__llvm_metadata(`nvvm.minctasm`=SIMDLength(1))
@__name(
    t"sm100_mha_1q_depth{config.depth}_{KVLUTType.dtype}_{output_type}_nqh{config.num_heads}_nkvh{config.num_heads // group}",
)
def _mha_sm100[
    KVLUTType: MHAOperand,
    output_type: DType,
    MaskType: MHAMask,
    SchedulerType: MHATileScheduler,
    config: MHAConfig,
    group: Int,
    ValidLengthType: OptionalPointer,
    SinkType: OptionalPointer,
    KVRowOffsetsType: OptionalPointer,
    _is_cache_length_accurate: Bool,
    MaxSeqLenType: OptionallyStaticInt,
    PartitionType: MHAPartitionScheme,
    swizzle_mode: TensorMapSwizzle,
](
    q_tma_op: QTMATile[
        KVLUTType.dtype,
        swizzle_mode,
        BM=config.block_m(),
        depth=config.depth,
        group=group,
        decoding=_is_decoding[MaxSeqLenType](),
    ],
    k_tma_op: KVTMATile[
        KVLUTType.dtype,
        swizzle_mode,
        BN=_kv_sub_tile_rows(config.block_n(), KVLUTType.page_size),
        BK=config.padded_depth,
    ],
    v_tma_op: KVTMATile[
        KVLUTType.dtype,
        swizzle_mode,
        BN=_kv_sub_tile_rows(config.block_n(), KVLUTType.page_size),
        BK=config.padded_depth,
    ],
    o_ptr_arg: UnsafePointer[Scalar[output_type], MutAnyOrigin],
    kv_lut: KVLUTType,
    scale: Float32,
    batch_size: UInt32,
    num_keys_arg: UInt32,
    pack: Pack[
        MaskType,
        SchedulerType,
        ValidLengthType,
        SinkType,
        KVRowOffsetsType,
        MaxSeqLenType,
        PartitionType,
    ],
):
    """MHA for token gen where seqlen = 1 and num_keys >= 1.

    The general data layout and steps conform to flash attention. Two exceptions:

    1 Partition across B, H, and num_keys (TODO). The last one is split-K and
      will need a separate reduction kernel at the end.

    2 First bmm becomes gemv and second bmm becomes gevm.
      TODO: use more optimized kernels for them

    """
    comptime kv_type = KVLUTType.dtype
    comptime assert kv_type == config.dtype
    comptime assert _is_decoding[MaxSeqLenType](), "mha_1q is decode-only"

    comptime simd_size: Int = simd_width_of[kv_type]()

    comptime num_softmax_threads: Int = config.num_consumer_threads()
    comptime num_softmax_warps = num_softmax_threads // 32

    comptime cta_group = 1
    comptime BM: Int = config.block_m()
    comptime BN: Int = config.block_n()
    comptime BK: Int = config.padded_depth
    comptime depth: Int = config.depth
    comptime padded_depth: Int = config.padded_depth
    # comptime mma_shape = Index(64, depth, 16)
    # comptime mma_shape = Index(128 if (BM % 128) == 0 else 64, depth, 16)
    # MMA_M here is defined as per-warp
    # comptime MMA_M = 64
    comptime MMA_M: Int = 128 if (BM % 128) == 0 else 64
    comptime MMA_N0: Int = BN
    comptime MMA_N1: Int = config.padded_depth
    comptime MMA_K: Int = 16 if kv_type.is_half_float() else 32
    # comptime WM = BM // num_softmax_warps
    # comptime WN = BN
    # comptime num_m_mmas = BM // MMA_M  # WM // MMA_M
    # mmas are now handled separately from in-register processing
    # in-register processing is divided up by warps, mmas are not
    comptime num_row_fragments = num_softmax_threads // 128
    comptime assert (32 % num_row_fragments) == 0
    comptime row_fragment_size = min(32 // num_row_fragments, BM // 4)
    comptime assert num_row_fragments * row_fragment_size <= 32
    comptime WM = row_fragment_size
    # if we have BM = 128, then we have
    # a 16x(BN//8) grid of 8x4<1x2>
    # this gives us 16 blocks to partition among rows.
    # Because we can load a minimum of 16 lanes at a time,
    # we prefer at least 2x blocks per warp, meaning we
    # can divide up to 8 ways.
    comptime num_m_blocks_per_warp = BM // (16 * num_softmax_warps)
    # before we had num_m_mmas * MMA_M = BM
    # now, we have num_m_blocks_per_warp * 16*num_softmax_warps == BM
    # num_m_blocks_per_warp is like `num_m_mmas`, but for non-mma consumers.
    comptime assert num_m_blocks_per_warp * 16 == WM
    #
    # The following constraint is effectively equivalent to
    # BM == 128 or BM == 64
    # If 32 // num_row_fragments is smaller, we have
    # 32*128*num_softmax_warps // num_softmax_threads == BM
    # 128*num_softmax_threads // num_softmax_threads == BM
    # 128 == BM
    # Or if BM // 4 is smaller, we have
    # BM // 4 * num_softmax_warps == BM
    # num_softmax_threads*BM // (4 * 32) == BM
    # num_softmax_threads == 128
    # 32*128 // BM == num_softmax_threads
    comptime assert WM * num_softmax_warps == BM
    # The above should also be true because:
    # num_softmax_warps = BM // (16 * num_m_blocks_per_warp)
    # -> BM // WM = BM // (16 * num_m_blocks_per_warp)
    # -> WM = (16 * num_m_blocks_per_warp)
    comptime num_m_mmas = 1
    comptime num_n_mmas = 1
    comptime num_k_mmas = BK // MMA_K
    # comptime num_warps_m = BM // WM  # 4 * num_softmax
    # comptime num_warps_n = BN // WN  # 1
    comptime num_heads: Int = config.num_heads
    # num_softmax_threads ignores the producers
    # actual number of threads is num_softmax_threads + 128
    comptime pipeline_stages = config.num_pipeline_stages
    var tid = UInt32(thread_idx.x)
    # warp group idx concept is still useful for sm100
    # because sets of 4 warps access tmem;
    # warp group idx gives index into sets of 16 lanes
    var warp_group_idx: UInt32 = warp.broadcast(tid // 128)
    # warp_group_tid = tid % 128
    comptime accum_type = get_accum_type[kv_type]()
    comptime assert (
        accum_type.is_floating_point()
    ), "accum_type must be floating point"

    # Fixed P scale of 256 (= 2^8) for FP8-QKV only. The
    # un-normalized softmax probabilities P sit in the e4m3 subnormal floor;
    # lifting them by 256 before the fp8 cast that feeds the P@V MMA reduces
    # PV-GEMM quantization error. P is produced by the SHARED
    # `_rowmax_online_softmax`/`_rowsum` helpers (also used by bf16/MLA), so
    # rather than bias those we scale `p_reg_tile` by 256 in-place right after
    # each rowmax-exp (fp8-guarded). `_rowsum` then reads the scaled P, and the
    # sink_contribution is scaled to match, so numerator (stored P, P@V) and
    # denominator (row_sum + sink) stay in the same 256x scale and cancel
    # through the final 1/row_sum normalize. Overflow-safe: max P after
    # row-max subtraction = exp2(0)*256 = 256 < 448 (e4m3 max). The scale is
    # applied ONLY inside `comptime if kv_type.is_float8()` branches below, so
    # the bf16 codegen is byte-identical (a `* 1.0` would otherwise survive as
    # a real fmul).
    comptime p_fp8_scale: Scalar[accum_type] = 256.0
    comptime max_tmem_cols = 512
    # When P can't fit alongside O in one TMEM bank, store P in SMEM
    # and use SS MMA for UMMA1 (P@V). S and O go in separate TMEM banks.
    comptime use_p_smem = padded_depth + MMA_N0 // 2 > max_tmem_cols
    comptime num_s = (
        max_tmem_cols
        // MMA_N0 if use_p_smem else (max_tmem_cols - (MMA_N0 // 2) - MMA_N1)
        // MMA_N0
    )
    comptime UMMA0Type = SM100TensorAccumulatorSS[
        kv_type,
        accum_type,
        MMA_M=MMA_M,  # 128
        MMA_N=MMA_N0,  # BN
        BM=BM,  # 128
        BN=BN,  # BN
        BK=BK,  # depth
        compute_BK=align_up(depth, MMA_K),
        num_softmax_threads=num_softmax_threads,
        swizzle_a=swizzle_mode,
        swizzle_b=swizzle_mode,
        transpose_b=True,
        pipeline_stages=num_s,
    ]
    # Second WGMMA is a
    # BM x BN tile of p_frag @ BN x depth tile of V
    # When use_p_smem: P is in SMEM, so UMMA1 becomes SS MMA.
    # MMA_N capped at 256 (hardware max for MMA_M=64), num_n_mmas=2.
    comptime UMMA1_MMA_N = min(MMA_N1, 256) if use_p_smem else MMA_N1
    # UMMA1Type: always TS (used by all existing code paths).
    comptime UMMA1Type = SM100TensorAccumulatorTS[
        kv_type,
        accum_type,
        MMA_M=MMA_M,
        MMA_N=UMMA1_MMA_N,  # depth
        BM=BM,
        BN=MMA_N1,  # depth
        BK=BN,  # BN
        num_softmax_threads=num_softmax_threads,
        swizzle_b=swizzle_mode,
        transpose_b=False,
    ]
    # UMMA1TypeSS: SS version for use_p_smem (P@V with P in SMEM).
    comptime UMMA1TypeSS = SM100TensorAccumulatorSS[
        kv_type,
        accum_type,
        MMA_M=MMA_M,
        MMA_N=UMMA1_MMA_N,
        BM=BM,
        BN=MMA_N1,  # depth (total output width)
        BK=BN,  # BN (inner dim = num keys per block)
        compute_BK=align_up(BN, MMA_K),
        num_softmax_threads=num_softmax_threads,
        swizzle_a=swizzle_mode,
        swizzle_b=swizzle_mode,
        transpose_b=False,
        pipeline_stages=1,
    ]
    var mask = pack.mask
    var scheduler = pack.scheduler
    var valid_length = pack.valid_length
    var sink_weights = pack.sink_weights
    var kv_input_row_offsets = pack.kv_input_row_offsets
    var max_seq_len = pack.max_seq_len
    var partition = pack.partition

    # var warp_x: UInt32 = warp_id % num_warps_n

    # first umma is BM x BK @ BK x BN
    # The entire query block (BM x depth) is tiled in shared memory.
    # The same region is later reused as the output tile (write_output)
    # via `q_smem.bitcast[output_type]`, so it must hold the wider of the
    # two roles. We require output_type to be at least as wide as the KV/Q
    # element type so the ratio below is >= 1; a future config violating
    # this must extend the allocation (or stop bitcasting).
    comptime assert size_of[output_type]() >= size_of[kv_type](), (
        "q_smem is bitcast to output_type in write_output; output_type"
        " must be at least as wide as the KV/Q element type"
    )
    comptime q_or_out_kv_elems = size_of[output_type]() // size_of[kv_type]()
    comptime q_smem_size = BM * padded_depth * q_or_out_kv_elems
    var q_smem = external_memory[
        Scalar[kv_type],
        address_space=.SHARED,
        alignment=128,
        name="mha_dynamic_shared_memory",
    ]()

    # P buffer in SMEM for depth=512 decoding (softmax writes P here for SS MMA)
    comptime p_smem_elems = BM * MMA_N0 if use_p_smem else 0
    var p_smem = q_smem + q_smem_size

    # We have `num_pipeline_stages` instances of each
    comptime kv_smem_size = config.kv_smem_size(True)
    var kv_smem = q_smem + q_smem_size + p_smem_elems

    # var head_idx: UInt32 = block_idx.y
    # var q_tile_idx: UInt32 = block_idx.x

    # q tile has valid shape q_tile_num_rows x depth
    # q_tile_num_rows could be less than BM when seqlen % BM != 0

    # p_frag_size is 2 * WM//8 * MMA_N//8
    # that is, we have a (WM//8) x (MMA_N//8) grid of 8x4<1x2> blocks
    # Each such block has 2 elements.
    comptime p_frag_size = BM * MMA_N0 // (
        num_softmax_threads * num_m_blocks_per_warp
    )
    comptime o_frag_size = BM * MMA_N1 // (
        num_softmax_threads * num_m_blocks_per_warp
    )
    comptime assert p_frag_size == 2 * (WM // 8) * (MMA_N0 // 8)
    comptime assert o_frag_size == 2 * (WM // 8) * (MMA_N1 // 8)
    comptime frag_simdwidth = 2
    comptime assert (
        BN * num_k_mmas * BM * MMA_K
        == BK
        * num_n_mmas
        * p_frag_size
        * num_softmax_threads
        * num_m_blocks_per_warp
    )

    comptime num_row_blocks_per_mma = 2
    # a umma.m64n32k16 `D` fragment looks like
    #
    # 0,1  4,5   8, 9  12,13
    # 2,3  6,7  10,11  14,15
    #
    # Each row/column has `p_frag_simdwidth`-sized vectors
    # (e.g. `4,5` is of size 2 = p_frag_simdwidth)
    # We have `num_row_blocks_per_mma` rows.
    # The total number of elements (16) equals `p_frag_size`.
    # The number of columns equals
    # `p_frag_size // (num_row_blocks_per_mma * p_frag_simdwidth)`
    #
    # This gives us the layout:
    #
    # Note the ordering of strides:
    # ((1, 3), (0, 2, 4))
    # comptime output_layout = Layout(
    #     IntTuple(
    #         IntTuple(num_row_blocks_per_mma, num_m_blocks_per_warp),
    #         IntTuple(
    #             p_frag_simdwidth,
    #             p_frag_size // (num_row_blocks_per_mma * p_frag_simdwidth),
    #             num_n_mmas,
    #         ),
    #     ),
    #     IntTuple(
    #         IntTuple(p_frag_simdwidth, p_frag_size),
    #         IntTuple(1, 2 * p_frag_simdwidth, num_m_blocks_per_warp * p_frag_size),
    #     ),
    # )
    # Vectorizing the layout:
    comptime element_layout = Layout.row_major(1, frag_simdwidth)
    comptime vec_output_row_shape = IntTuple(num_row_blocks_per_mma, num_m_mmas)
    comptime p_vec_output_layout = Layout(
        IntTuple(
            vec_output_row_shape,
            IntTuple(
                p_frag_size // (num_row_blocks_per_mma * frag_simdwidth),
                num_n_mmas,
            ),
        ),
        IntTuple(
            IntTuple(frag_simdwidth, p_frag_size),
            IntTuple(
                num_row_blocks_per_mma * frag_simdwidth,
                num_m_mmas * p_frag_size,
            ),
        ),
    )
    comptime o_vec_output_layout = Layout(
        IntTuple(
            vec_output_row_shape,
            IntTuple(
                o_frag_size // (num_row_blocks_per_mma * frag_simdwidth),
                num_n_mmas,
            ),
        ),
        IntTuple(
            IntTuple(frag_simdwidth, o_frag_size),
            IntTuple(
                num_row_blocks_per_mma * frag_simdwidth,
                num_m_mmas * o_frag_size,
            ),
        ),
    )
    comptime num_rows_per_warp = p_vec_output_layout[0].size()
    comptime num_cols_p = p_vec_output_layout[1].size()
    comptime num_cols_output = o_vec_output_layout[1].size()

    # Rowwise max and sum for online softmax
    comptime accum_simd_width = simd_width_of[accum_type]()
    comptime row_alignment = align_of[SIMD[accum_type, accum_simd_width]]()
    # Account for group query.
    comptime kv_num_heads = num_heads // group

    # var lane_predicate = elect_one_sync() # not needed with async_copy

    comptime mma_thread_layout = Layout.row_major(8, 4)
    comptime ragged = not ValidLengthType.is_null

    # Handle sink_weights
    # SAFETY: Only dereferenced when SinkType is non-null (comptime guard
    # below), at which point it's overwritten with the real pointer.
    var sink_weights_ptr = UnsafePointer[
        Scalar[kv_type], ImmutAnyOrigin
    ].unsafe_dangling()

    comptime if not SinkType.is_null:
        sink_weights_ptr = rebind[
            UnsafePointer[Scalar[kv_type], ImmutAnyOrigin]
        ](sink_weights.value())

    # actually 16 byte alignment
    var produced_mbar_kv = (kv_smem + kv_smem_size).bitcast[SharedMemBarrier]()
    var producer_mbar_kv = produced_mbar_kv + pipeline_stages  # 16
    var mma_mbar = producer_mbar_kv + pipeline_stages  # 16
    var umma_0 = UMMA0Type(mma_mbar.as_unsafe_any_origin())  # needs num_s
    # umma_1: TS for non-use_p_smem, SS for use_p_smem (both use same barrier layout)
    var umma_1_ts = UMMA1Type((mma_mbar + 2 * num_s).as_unsafe_any_origin())
    var umma_1_ss = UMMA1TypeSS((mma_mbar + 2 * num_s).as_unsafe_any_origin())
    # P SMEM consumption barrier: warp 1 arrives after SS MMA finishes
    # reading P from SMEM, softmax waits before overwriting P SMEM.
    var ptr_tmem_addr = (mma_mbar + 2 * num_s + 2).bitcast[UInt32]()  # 8

    comptime USE_TMA = True
    # https://github.com/Dao-AILab/flash-attention/blob/3b5047d2ce742848f45d44b143d511f211eba2d2/hopper/flash_fwd_kernel_sm90.h#L81-L82
    # comptime num_producer_regs = 56 if num_softmax_warps == 4 else (
    #     (24 if USE_TMA else 56) if num_softmax_warps == 8 else 32
    # )
    # comptime num_softmax_regs = 256 if num_softmax_warps == 4 else (
    #     (240 if USE_TMA else 224) if num_softmax_warps == 8 else 160
    # )
    comptime num_producer_regs = 56
    comptime num_softmax_regs = 224

    # constructing calls barrier() if static
    # Use the launched (max) partition count so block_idx decodes into
    # [0, max_num_partitions()); CTAs with index >= num_partitions() are
    # over-launched and early-return below.
    var tile_summary = MHATileSummary[ValidLengthType](
        batch_size,
        ceildiv(max_seq_len.as_uint32(), UInt32(BM))
        * partition.max_num_partitions(),
        valid_length,
        max_seq_len.as_uint32(),
    )
    var state: MHATileState = scheduler.initial_state(
        (ptr_tmem_addr + 2).as_unsafe_any_origin(), tile_summary
    )

    # The persistent kernels limit the grid size.
    # initial_seq_info = scheduler.unsafe_get_current_work_info(tile_summary, state)

    var initial_seq_info = scheduler.unsafe_seq_info(tile_summary, state)
    comptime assert not SchedulerType.may_advance

    if tid == 0:
        comptime for i in range(pipeline_stages):
            # until we can use TMA, we need 128 producers working on async copies
            produced_mbar_kv[i].init(1)
            producer_mbar_kv[i].init(Int32(num_softmax_threads))
        umma_0.init()
        # Always use TS barrier protocol for UMMA1 (simple phase tracking).
        # SS pipeline management causes phase desync for single-stage UMMA1.
        umma_1_ts.init()

    comptime PositionType = MHAPosition[
        BM,
        BN,
        depth,
        padded_depth,
        num_heads,
        group,
        _is_decoding[MaxSeqLenType](),
    ]

    @__parameter
    @always_inline
    def get_position(seq_info: SeqInfo) -> PositionType:
        return _get_position[
            BM,
            BN,
            depth,
            padded_depth,
            num_heads,
            group,
            ragged,
            _is_cache_length_accurate,
        ](
            seq_info,
            kv_lut,
            max_seq_len,
            num_keys_arg,
            kv_input_row_offsets,
        )

    var position: PositionType = get_position(initial_seq_info)
    var startend = position.get_start_and_end_for_partitions[
        page_size=KVLUTType.page_size
    ](partition, mask)
    var kv_tile_start_row: UInt32 = startend[0]
    var end: UInt32 = startend[1]

    comptime assert num_s > 0

    barrier()

    # Programmatic Dependent Launch. This barrier is the last point every CTA
    # reaches before the warp-specialized `do_partition` early-returns below,
    # so it is the divergence-free place to honor the launch-dependents
    # contract for every CTA (a producer CTA that skipped it would hang a
    # waiting consumer such as `mha_splitk_reduce`). `wait` overlaps this
    # grid's prologue with its predecessor's tail; `launch` lets the dependent
    # reduce grid be admitted early. No-op on non-SM90+ / when MHA_PDL=off.
    comptime if MHA_PDL_LEVEL > PDLLevel.OFF:
        wait_on_dependent_grids()
        launch_dependent_grids()

    # The grid is launched with `max_num_partitions()` CTAs per (head, batch) so
    # its shape is independent of num_keys (one CUDA graph per batch size). The
    # tail CTAs with index >= `num_partitions()` carry no keys: exit now — after
    # honoring the PDL contract above, and before any TMEM allocation or
    # exp_sum/qk_max/output write (so buffers need only `num_partitions()` slots).
    # `prompt_offset` is decoded from block_idx, hence uniform across the CTA, so
    # the whole CTA returns together and no warp is left waiting on a later
    # `named_barrier`. Partitions below this bound that are empty due to BN
    # alignment still take the writeback path below so the reducer sees them.
    comptime if PartitionType.do_partition:
        if position.prompt_offset >= partition.num_partitions():
            return

    # For intra-warp overlap, we initiate ummas as
    # Q @ K_0, Q @ K_1, P_0 @ V_0, Q @ K_2, P_1 @ V_1, ...
    # ..., Q @ K_{N-1}, P_{N-2} @ V_{N-2}, P_{N-1} @ V_{N-1}
    #
    # Due to this, we can overlap ummas and softmax calculations.
    if warp_group_idx == 0:
        # producer
        warpgroup_reg_dealloc[num_producer_regs]()
        if tid == 96:  # thread 0 of warp id 3
            produce[
                swizzle_mode,
                pipeline_stages=pipeline_stages,
                ragged=ragged,
                _is_cache_length_accurate=_is_cache_length_accurate,
            ](
                q_tma_op,
                k_tma_op,
                v_tma_op,
                q_smem,
                kv_smem,
                produced_mbar_kv,
                producer_mbar_kv,
                None,
                None,
                kv_lut,
                position,
                partition,
                scheduler,
                mask,
                tile_summary,
                state,
                max_seq_len,
                num_keys_arg,
                kv_input_row_offsets,
            )
        elif warp_id() == 0:  # warp id == 0: Q @ K'
            startend = position.get_start_and_end_for_partitions[
                page_size=KVLUTType.page_size
            ](partition, mask)
            var kv_tile_start_row: UInt32 = startend[0]
            var end: UInt32 = startend[1]

            comptime if PartitionType.do_partition:
                # we exit before allocating so we don't need to deallocate
                if kv_tile_start_row >= end:
                    return

            comptime if use_p_smem:
                # Two-bank: bank 0 = O (depth cols), bank 1 = S (num_s*BN cols)
                comptime assert num_s * MMA_N0 <= max_tmem_cols
                comptime assert MMA_N1 <= max_tmem_cols
            else:
                comptime tmem_cols = num_s * MMA_N0 + (MMA_N0 // 2) + MMA_N1
                comptime assert tmem_cols <= max_tmem_cols
            tcgen05_alloc[cta_group](ptr_tmem_addr, max_tmem_cols)

            var qk_desc = UMMA0Type.mma_descriptors(
                q_smem.as_unsafe_any_origin(), kv_smem.as_unsafe_any_origin()
            )

            named_barrier[Int32(num_softmax_threads + 2 * WARP_SIZE)]()
            if tid != 0:
                return
            var q_desc = qk_desc.get_a()
            var k_desc = qk_desc.get_b()
            var tmem_addr: UInt32 = ptr_tmem_addr[0]
            var s_tmem: UInt32
            # var o_tmem: UInt32
            comptime if use_p_smem:
                # o_tmem = tmem_addr  # bank 0
                s_tmem = tmem_addr + UInt32(1 << 20)  # bank 1
            else:
                s_tmem = tmem_addr
                # o_tmem = tmem_addr + UInt32(MMA_N0 * num_s)
            var s_accumulator = UMMA0Type.c_t(s_tmem)

            @__parameter
            @always_inline
            def q_mul_k(read_idx: UInt32, read_phase: UInt32):
                var q = q_desc
                var k = k_desc + Int(
                    UInt32(BN * config.padded_depth * size_of[kv_type]())
                    * read_idx
                )
                umma_0.wait_for_tmem()
                produced_mbar_kv[read_idx].wait(read_phase)

                umma_0.mma(
                    rebind[UMMA0Type.a_t](q),
                    rebind[UMMA0Type.b_t](k),
                    s_accumulator,
                    0,
                )

            var mask_status: TileMaskStatus
            while True:
                mask_status = position.mask_status(mask, kv_tile_start_row)
                if mask_status != TileMaskStatus.FULL_MASK:
                    break
                kv_tile_start_row += UInt32(BN)

            var kv_pipeline_states = PipelineState[pipeline_stages]()
            # s_pipeline_states = PipelineState[pipeline_stages]()
            q_mul_k(
                kv_pipeline_states.index(),
                kv_pipeline_states.phase(),
            )
            kv_pipeline_states.step()

            # Consumption order:
            # Preheader: Q0, K0
            # Body: Q1, K1, V0, Q2, K2, V1, ..., Q{-1}, K{-1}, V{-2}
            # Exit: V{-1}
            while True:
                # this loops over num_keys
                kv_tile_start_row += UInt32(BN)
                if kv_tile_start_row >= end:
                    break
                # this loops over num_keys
                mask_status = position.mask_status(mask, kv_tile_start_row)
                if mask_status == TileMaskStatus.FULL_MASK:
                    continue

                # new pipeline states
                # start ummas
                q_mul_k(
                    kv_pipeline_states.index(), kv_pipeline_states.phase()
                )  # can't rw `p_reg_tile`
                kv_pipeline_states.step()
                kv_pipeline_states.step()

        elif warp_id() == 1:  # warp id 1: P @ V
            startend = position.get_start_and_end_for_partitions[
                page_size=KVLUTType.page_size
            ](partition, mask)
            var kv_tile_start_row: UInt32 = startend[0]
            var end: UInt32 = startend[1]

            comptime if PartitionType.do_partition:
                if kv_tile_start_row >= end:
                    return

            named_barrier[Int32(num_softmax_threads + 2 * WARP_SIZE)]()
            var tmem_addr: UInt32 = ptr_tmem_addr[0]
            if tid == 32:
                var s_tmem: UInt32 = 0
                var o_tmem: UInt32 = 0
                var p_tmem: UInt32 = 0

                @__parameter
                @always_inline("nodebug")
                def p_mul_v(
                    read_idx: UInt32,
                    read_phase: UInt32,
                    scale_c: UInt32,
                    kv_row: UInt32,
                ):
                    comptime offset_elems_per = BN * config.padded_depth
                    comptime offset_bytes_per = offset_elems_per * size_of[
                        kv_type
                    ]()
                    comptime if use_p_smem:
                        comptime assert UMMA1TypeSS.num_n_mmas == 2
                        o_tmem = tmem_addr  # bank 0
                        var output_accumulator = UMMA1Type.c_t(o_tmem)
                        s_tmem = tmem_addr + UInt32(1 << 20)  # bank 1
                        # SS MMA: both P and V from SMEM
                        var pv_descs = UMMA1TypeSS.mma_descriptors(
                            p_smem.as_unsafe_any_origin(),
                            kv_smem.as_unsafe_any_origin(),
                        )
                        var p_desc_a = pv_descs.get_a()
                        var v_desc = pv_descs.get_b()
                        var v = v_desc + Int(
                            UInt32(offset_bytes_per) * read_idx
                        )
                        umma_1_ts.wait_for_tmem()
                        produced_mbar_kv[read_idx].wait(read_phase)
                        umma_1_ss.mma(
                            rebind[UMMA1TypeSS.a_t](p_desc_a),
                            rebind[UMMA1TypeSS.b_t](v),
                            rebind[UMMA1TypeSS.c_t](output_accumulator),
                            scale_c,
                        )
                        # Fence ensures SS MMA has finished reading P
                        # from SMEM before we signal P SMEM is free.
                        tcgen05_fence_after()
                    else:
                        s_tmem = tmem_addr
                        o_tmem = tmem_addr + UInt32(MMA_N0 * num_s)
                        var output_accumulator = UMMA1Type.c_t(o_tmem)
                        p_tmem = (
                            tmem_addr + UInt32(MMA_N0 * num_s) + UInt32(MMA_N1)
                        )
                        var p_desc = UMMA1Type.a_mma_descriptor(p_tmem)
                        var v_desc = UMMA1Type.b_mma_descriptor(
                            kv_smem.as_unsafe_any_origin()
                        )
                        var v = v_desc + Int(
                            UInt32(offset_bytes_per) * read_idx
                        )
                        umma_1_ts.wait_for_tmem()
                        produced_mbar_kv[read_idx].wait(read_phase)
                        umma_1_ts.mma(
                            rebind[UMMA1Type.a_t](p_desc),
                            rebind[UMMA1Type.b_t](v),
                            output_accumulator,
                            scale_c,
                        )

                var mask_status: TileMaskStatus
                while True:
                    mask_status = position.mask_status(mask, kv_tile_start_row)
                    if mask_status != TileMaskStatus.FULL_MASK:
                        break
                    kv_tile_start_row += UInt32(BN)

                var kv_pipeline_states = PipelineState[pipeline_stages]()
                kv_pipeline_states.step()
                comptime assert pipeline_stages >= 2

                var output_scale: UInt32 = 0
                # Consumption order:
                # Preheader: Q0, K0
                # Body: Q1, K1, V0, Q2, K2, V1, ..., Q{-1}, K{-1}, V{-2}
                # Exit: V{-1}
                while True:
                    # this loops over num_keys
                    kv_tile_start_row += UInt32(BN)
                    if kv_tile_start_row >= end:
                        break
                    # this loops over num_keys
                    mask_status = position.mask_status(mask, kv_tile_start_row)
                    if mask_status == TileMaskStatus.FULL_MASK:
                        continue

                    # copy new pfrag, used by `p_mul_v` on next iter
                    # start ummas
                    kv_pipeline_states.step()
                    # read_idx_v = 0, phase = 1
                    p_mul_v(
                        kv_pipeline_states.index(),
                        kv_pipeline_states.phase(),
                        output_scale,
                        0,
                    )  # can't rw output or pfrag
                    output_scale = 1
                    kv_pipeline_states.step()

                p_mul_v(
                    kv_pipeline_states.index(),
                    kv_pipeline_states.phase(),
                    output_scale,
                    kv_tile_start_row,
                )
            tcgen05_release_allocation_lock[cta_group]()
            tcgen05_dealloc[cta_group](tmem_addr, max_tmem_cols)

    else:  # softmax
        warpgroup_reg_alloc[num_softmax_regs]()

        # arrive to unblock the producers
        # TODO: skip this by not waiting on the first set
        comptime for i in range(pipeline_stages):
            _ = producer_mbar_kv[i].arrive()
        umma_0.tmem_arrive_init()
        # Bootstrap producer_p: first P write has no prior MMA to wait for.
        # Only one thread arrives (barrier expects 1 arrival).

        var warp_id: UInt32 = warp.broadcast((tid - 128) // UInt32(WARP_SIZE))

        # Coordinates of the current warp.
        var elect_one_warp = warp_id == 0

        var lane = UInt32(lane_id())

        var warp_y: UInt32 = warp_id  # // num_warps_n

        comptime if num_softmax_threads > 128:
            warp_y = 2 * (warp_y % 4) + (warp_y // 4)
        comptime warp_x: UInt32 = 0
        comptime assert num_softmax_warps == 4 or num_softmax_warps == 8

        # Mask global memory iterator.

        var mask_warp_row = warp_y * UInt32(WM)
        var scale_log2e: Scalar[accum_type] = (
            scale.cast[
                accum_type
            ]() if MaskType.apply_log2e_after_mask else scale.cast[accum_type]()
            * log2e
        )

        # layout is
        # shape  = (2, num_m_blocks_per_warp) x (2, num_n_mmas)
        # stride = (2, 4*num_n_mmas) x (1, 4)

        var rowmax = LayoutTensor[
            UMMA0Type.accum_t,
            Layout.row_major(num_rows_per_warp),
            MutAnyOrigin,
            address_space=.LOCAL,
        ].stack_allocation()
        var rowsum = LayoutTensor[
            UMMA0Type.accum_t,
            Layout.row_major(num_rows_per_warp),
            MutAnyOrigin,
            address_space=.LOCAL,
        ].stack_allocation()
        comptime VecPType = LayoutTensor[
            accum_type,
            p_vec_output_layout,
            MutAnyOrigin,
            address_space=.LOCAL,
            element_layout=element_layout,
        ]
        comptime VecOType = LayoutTensor[
            accum_type,
            o_vec_output_layout,
            MutAnyOrigin,
            address_space=.LOCAL,
            element_layout=element_layout,
        ]

        var p_reg_tile = UMMA0Type.c_t.allocate_register_tile()
        var output_reg_tile = UMMA1Type.c_t.allocate_register_tile()

        @__parameter
        @always_inline
        def vectorize_p_reg_tile(
            out result: VecPType,
        ):
            result = {p_reg_tile.ptr}

        @__parameter
        @always_inline
        def vectorize_o_reg_tile(
            out result: VecOType,
        ):
            result = {output_reg_tile.ptr}

        @__parameter
        @always_inline
        def scale_p_for_fp8():
            # Apply the cuDNN-style 256x P scale in-place on p_reg_tile (fp8
            # only; comptime-dead for bf16 where p_fp8_scale == 1.0). Called
            # right after `_rowmax_online_softmax` exponentiates P and BEFORE
            # `_rowsum`, so the row-sum sees the same 256x as the stored P.
            comptime if kv_type.is_float8():
                var vp = vectorize_p_reg_tile()
                var s = SIMD[accum_type, element_layout.size()](p_fp8_scale)
                comptime for row in range(num_rows_per_warp):
                    comptime for col in range(num_cols_p):
                        vp[row, col] = vp[row, col] * s

        @__parameter
        @always_inline
        def apply_mask(
            position: PositionType,
            mask_status: TileMaskStatus,
            kv_tile_start_row: UInt32,
        ):
            var max_len: UInt32 = num_keys_arg
            _apply_mask[WM, MMA_N0, num_m_mmas, num_n_mmas](
                mask_warp_row,
                position,
                lane,
                max_len,
                scale_log2e,
                kv_tile_start_row,
                mask,
                mask_status,
                vectorize_p_reg_tile(),
            )

        @__parameter
        @always_inline
        def scale(correction: type_of(rowmax), vout: VecOType):
            # Correct output
            # We could avoid this on the first iter
            # if we specialize and unswitch on `first_iter`
            # otherwise, the branch requires synchronization
            comptime for row in range(num_rows_per_warp):
                var c = SIMD[accum_type, element_layout.size()](
                    rebind[Scalar[accum_type]](correction[row])
                )

                comptime for col in range(num_cols_output):
                    vout[row, col] = vout[row, col] * c

        @__parameter
        @always_inline
        def elementwise_reciprocal(
            old_rowsum: type_of(rowsum), new_rowsum: type_of(rowsum)
        ):
            # new_rowsum, old_rowsum = 1/old_rowsum, new_rowsum
            comptime for row in range(num_rows_per_warp):
                var old = old_rowsum[row]
                var new = new_rowsum[row]
                new_rowsum[row] = recip(old)[0]
                old_rowsum[row] = new

        @__parameter
        @always_inline
        def write_output(
            position: PositionType,
            rowsum_inv: type_of(rowsum),
            vout: VecOType,
        ):
            # Apply softmax denumerator.
            comptime for row in range(num_rows_per_warp):
                var rs_inv = vout.element_type(rowsum_inv[row][0])

                comptime for col in range(num_cols_output):
                    vout[row, col] = vout[row, col] * rs_inv

            var output_ptr: UnsafePointer[
                Scalar[output_type], MutAnyOrigin
            ] = o_ptr_arg

            comptime if PartitionType.do_partition:
                output_ptr = output_ptr + (
                    UInt32(depth * num_heads)
                    * batch_size
                    * position.prompt_offset
                )
            var output_gmem_tile = position.q_out_gmem_tensor(output_ptr)

            # Write to global memory.
            comptime assert (
                output_type.is_half_float()
            ), "we don't support Float32 output"
            comptime assert size_of[kv_type]() <= size_of[output_type]()
            comptime swizzle = make_swizzle[
                num_rows=WM // 2, row_size=BN, access_size=8
            ]()
            # Reuse a_smem for c tile in smem
            var accum_smem_tile = LayoutTensor[
                output_type,
                Layout.row_major(BM, config.padded_depth),
                address_space=.SHARED,
            ]((q_smem).bitcast[Scalar[output_type]]())
            var accum_smem_warp_tile = accum_smem_tile.tile[WM, BN](
                Int(warp_y), Int(warp_x)
            )

            # ensure all threads have finished reading `q_smem`
            named_barrier[Int32(num_softmax_threads)]()

            copy_local_to_shared[
                thread_layout=mma_thread_layout, swizzle=swizzle
            ](
                accum_smem_warp_tile.vectorize[1, 2](),
                UMMA1Type.c_t.rows_of_frags(output_reg_tile)
                .vectorize[1, 2]()
                .transpose(),
            )
            fence_async_view_proxy()
            # Guard writing to shared memory.
            named_barrier[Int32(num_softmax_threads)]()
            # Vectorized copy from shared to global memory, during which every 2 FP32
            # are cast to 2 BF16 so that 2 4xFP32 vectors are merged into 1 8xBF16
            # vector and stored using 16B store instruction.
            comptime out_simd_size = simd_width_of[output_type]()
            copy_sram_to_dram[
                thread_layout=Layout.row_major(
                    num_softmax_threads * out_simd_size // depth,
                    depth // out_simd_size,
                ),
                swizzle=swizzle,
            ](
                output_gmem_tile.vectorize[1, out_simd_size](),
                accum_smem_tile.vectorize[1, out_simd_size](),
            )

        comptime if PartitionType.do_partition:  # we may have an empty partition
            if kv_tile_start_row >= end:
                if umod(thread_idx.x, 4) == 0 and thread_idx.x < (
                    4 * min(group, 8) + 128
                ):
                    var exp_sum_ptr, qk_max_ptr = position.exp_sum_qk_max_ptr(
                        partition, batch_size
                    )
                    var q_heads = get_q_head_idx(position, lane)

                    comptime for i in range(q_heads.size):
                        var q_head_idx = q_heads[i]
                        exp_sum_ptr[q_head_idx] = Scalar[
                            PartitionType.accum_dtype
                        ](0)
                        qk_max_ptr[q_head_idx] = min_or_neg_inf[
                            PartitionType.accum_dtype
                        ]()

                write_output(position, rowsum, vectorize_o_reg_tile().fill(0))
                return

        named_barrier[Int32(num_softmax_threads + 2 * WARP_SIZE)]()
        var tmem_addr = ptr_tmem_addr[0]

        var s_tmem: UInt32
        var o_tmem: UInt32
        var p_tmem: UInt32 = 0
        comptime if use_p_smem:
            o_tmem = tmem_addr  # bank 0
            s_tmem = tmem_addr + UInt32(1 << 20)  # bank 1
        else:
            comptime if num_softmax_warps > 4:
                if warp_group_idx != 1:  # elect_one_warp will be false
                    tmem_addr += 1 << 20
            s_tmem = tmem_addr
            o_tmem = tmem_addr + UInt32(MMA_N0 * num_s)
            p_tmem = tmem_addr + UInt32(MMA_N0 * num_s) + UInt32(MMA_N1)
        var p_accumulator = UMMA0Type.c_t(s_tmem)
        # Use UMMA1Type.c_t for output_accumulator in all branches
        # (both TS and SS have the same c_t shape since MMA_N was aligned)
        var output_accumulator = UMMA1Type.c_t(o_tmem)
        var p_desc = UMMA1Type.a_mma_descriptor(p_tmem)

        @__parameter
        @always_inline
        def wait_for_q_mul_k(read_idx: UInt32):
            var p_acc = umma_0.wait_for_mma(p_accumulator)  # P is available
            _ = producer_mbar_kv[read_idx].arrive()
            comptime if use_p_smem:
                tcgen05_fence_after()
            p_acc.copy_to(p_reg_tile)
            umma_0.tmem_arrive()

        @__parameter
        @always_inline
        def wait_for_p_mul_v(read_idx: UInt32):
            umma_1_ts.wait_for_mma()  # output is available
            _ = producer_mbar_kv[read_idx].arrive()
            comptime if use_p_smem:
                tcgen05_fence_after()
            output_accumulator.copy_to(output_reg_tile)

        var mask_status: TileMaskStatus
        while True:
            mask_status = position.mask_status(mask, kv_tile_start_row)
            if mask_status != TileMaskStatus.FULL_MASK:
                break
            kv_tile_start_row += UInt32(BN)

        var kv_pipeline_states = PipelineState[pipeline_stages]()
        # q_mul_k must wait on fetching q and k
        # therefore, we find `kv_tile_start_row` first.
        var read_idx_q: UInt32 = kv_pipeline_states.index()
        # q_mul_k(
        #     read_idx_q,
        #     kv_pipeline_states.phase(),
        # )
        kv_pipeline_states.step()

        wait_for_q_mul_k(read_idx_q)
        apply_mask(position, mask_status, kv_tile_start_row)

        comptime if not SinkType.is_null:
            # Include sink_weights in rowmax computation if present
            var q_head_indices = get_q_head_idx(position, lane)

            comptime for i in range(q_head_indices.size):
                var head_idx = q_head_indices[i]
                var sink_weight = sink_weights_ptr[head_idx] * log2e
                rowmax[i] = sink_weight.cast[accum_type]()

        # Compute initial rowmax
        var attention_rowmax = _rowmax_online_softmax[
            1, mma_thread_layout, use_exp2=True
        ](vectorize_p_reg_tile(), rowmax, init_rowmax=SinkType.is_null)

        rowmax.copy_from(attention_rowmax)

        # Lift P out of the e4m3 subnormal floor (fp8 only) before rowsum and
        # the fp8 cast feeding P@V. See p_fp8_scale comment above.
        scale_p_for_fp8()

        comptime assert p_vec_output_layout.size() > 0, "layout: " + String(
            p_vec_output_layout
        )

        # Compute rowsum
        var attention_rowsum = _rowsum[mma_thread_layout](
            vectorize_p_reg_tile()
        )

        # Add sink weight contribution to rowsum
        comptime if not SinkType.is_null:
            var q_head_indices = get_q_head_idx(position, lane)

            comptime for i in range(q_head_indices.size):
                var head_idx = q_head_indices[i]
                var sink_weight = (
                    sink_weights_ptr[head_idx].cast[accum_type]() * log2e
                )
                # Scale the sink mass to match the 256x-lifted P so it cancels
                # through 1/row_sum. The `comptime if kv_type.is_float8()`
                # keeps the bf16 sink expression byte-identical (no `* 1.0`).
                var sink_contribution: type_of(exp2(sink_weight - rowmax[i]))
                comptime if kv_type.is_float8():
                    sink_contribution = (
                        exp2(sink_weight - rowmax[i]) * p_fp8_scale
                    )
                else:
                    sink_contribution = exp2(sink_weight - rowmax[i])
                attention_rowsum[i] += sink_contribution[0]

        rowsum.copy_from(attention_rowsum)

        # var output_scale: UInt32 = 0
        # Consumption order:
        # Preheader: Q0, K0
        # Body: Q1, K1, V0, Q2, K2, V1, ..., Q{-1}, K{-1}, V{-2}
        # Exit: V{-1}
        while True:
            # this loops over num_keys
            kv_tile_start_row += UInt32(BN)
            if kv_tile_start_row >= end:
                break
            # this loops over num_keys
            mask_status = position.mask_status(mask, kv_tile_start_row)
            if mask_status == TileMaskStatus.FULL_MASK:
                continue

            named_barrier[Int32(num_softmax_threads)](5)

            # copy new pfrag, used by `p_mul_v` on next iter
            comptime if use_p_smem:
                # Wait for warp 1's SS MMA to finish reading P from SMEM
                # before we overwrite it.
                comptime p_swizzle = (
                    make_swizzle[
                        kv_type, TensorMapSwizzle.SWIZZLE_64B
                    ]() if kv_type.is_float8() else make_swizzle[
                        num_rows=WM // 2, row_size=MMA_N0, access_size=8
                    ]()
                )
                var p_smem_tile = LayoutTensor[
                    kv_type,
                    Layout.row_major(BM, MMA_N0),
                    address_space=.SHARED,
                ](p_smem.bitcast[Scalar[kv_type]]())
                var p_smem_warp_tile = p_smem_tile.tile[WM, MMA_N0](
                    Int(warp_y), 0
                )
                copy_local_to_shared[
                    thread_layout=mma_thread_layout, swizzle=p_swizzle
                ](
                    p_smem_warp_tile.vectorize[1, 2](),
                    UMMA0Type.c_t.rows_of_frags(p_reg_tile)
                    .vectorize[1, 2]()
                    .transpose(),
                )
                fence_async_view_proxy()
                named_barrier[Int32(num_softmax_threads)]()
                umma_1_ts.tmem_arrive()
            else:
                p_desc.copy_from(UMMA0Type.c_t.rows_of_frags(p_reg_tile))
                umma_1_ts.tmem_arrive()

            # new pipeline states
            var read_idx_q: UInt32 = kv_pipeline_states.index()
            # start ummas
            # q_mul_k(
            #     read_idx_q, kv_pipeline_states.phase()
            # )  # can't rw `p_reg_tile`
            kv_pipeline_states.step()
            var read_idx_v: UInt32 = kv_pipeline_states.index()
            # p_mul_v(
            #     read_idx_v, kv_pipeline_states.phase(), output_scale
            # )  # can't rw output or pfrag
            # output_scale = 1
            kv_pipeline_states.step()
            wait_for_q_mul_k(read_idx_q)

            apply_mask(position, mask_status, kv_tile_start_row)
            # Compute rowmax for current scores
            var current_rowmax = _rowmax_online_softmax[
                1, mma_thread_layout, use_exp2=True
            ](vectorize_p_reg_tile(), rowmax, False)

            # Lift this iteration's P by 256x (fp8 only) before its rowsum and
            # the fp8 cast feeding P@V, matching the initial tile. The running
            # `rowsum` accumulates these 256x-scaled per-tile sums, and the
            # 256x P@V output is normalized by 1/rowsum -> exact cancel.
            scale_p_for_fp8()

            var score_frag_rowmax = current_rowmax
            var score_frag_rowsum = rebind[type_of(rowsum)](
                _rowsum[mma_thread_layout](vectorize_p_reg_tile())
            )

            _online_softmax_correction[use_exp2=True](rowmax, score_frag_rowmax)
            # rowmax now holds score_frag_rowmax
            # score_frag_rowmax now holds the correction

            comptime for i in range(num_rows_per_warp):
                rowsum[i] = (
                    rowsum[i] * score_frag_rowmax[i] + score_frag_rowsum[i]
                )

            wait_for_p_mul_v(read_idx_v)  # can rw output and pfrag
            scale(score_frag_rowmax, vectorize_o_reg_tile())  # scale output
            output_accumulator.copy_from(output_reg_tile)

        # Final P write
        comptime if use_p_smem:
            # Wait for warp 1's SS MMA to finish reading P from SMEM
            comptime p_swizzle = (
                make_swizzle[
                    kv_type, TensorMapSwizzle.SWIZZLE_64B
                ]() if kv_type.is_float8() else make_swizzle[
                    num_rows=WM // 2, row_size=MMA_N0, access_size=8
                ]()
            )
            var p_smem_tile = LayoutTensor[
                kv_type,
                Layout.row_major(BM, MMA_N0),
                address_space=.SHARED,
            ](p_smem.bitcast[Scalar[kv_type]]())
            var p_smem_warp_tile = p_smem_tile.tile[WM, MMA_N0](Int(warp_y), 0)
            copy_local_to_shared[
                thread_layout=mma_thread_layout, swizzle=p_swizzle
            ](
                p_smem_warp_tile.vectorize[1, 2](),
                UMMA0Type.c_t.rows_of_frags(p_reg_tile)
                .vectorize[1, 2]()
                .transpose(),
            )
            fence_async_view_proxy()
            named_barrier[Int32(num_softmax_threads)](6)
            umma_1_ts.tmem_arrive()
        else:
            p_desc.copy_from(UMMA0Type.c_t.rows_of_frags(p_reg_tile))
            umma_1_ts.tmem_arrive()

        # p_mul_v(
        #     kv_pipeline_states.index(),
        #     kv_pipeline_states.phase(),
        #     output_scale,
        # )

        comptime if PartitionType.do_partition:
            # Only the first thread of each row
            if umod(thread_idx.x, 4) == 0 and thread_idx.x < (
                4 * min(group, 8) + 128
            ):
                var exp_sum_ptr, qk_max_ptr = position.exp_sum_qk_max_ptr(
                    partition, batch_size
                )
                var q_heads = get_q_head_idx(position, lane)

                comptime for i in range(q_heads.size):
                    var q_head_idx = q_heads[i]
                    exp_sum_ptr[q_head_idx] = rebind[
                        Scalar[PartitionType.accum_dtype]
                    ](rowsum[i])
                    qk_max_ptr[q_head_idx] = rebind[
                        Scalar[PartitionType.accum_dtype]
                    ](rowmax[i])

        comptime for row in range(num_rows_per_warp):
            rowsum[row] = recip(rowsum[row])[0]

        umma_1_ts.wait_for_mma()
        comptime if use_p_smem:
            tcgen05_fence_after()
        output_accumulator.copy_to(output_reg_tile)

        comptime assert type_of(output_reg_tile).layout[1].size() > 1, (
            "output_reg_tile.layout = "
            + String(type_of(output_reg_tile).layout)
            + "\n"
        )
        write_output(position, rowsum, vectorize_o_reg_tile())
        # don't arrive
