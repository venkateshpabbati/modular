# ===----------------------------------------------------------------------=== #
# Copyright (c) 2026, Modular Inc. All rights reserved.
#
# Licensed under the Apache License v2.0 with LLVM Exceptions:
# https://llvm.org/LICENSE.txt
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ===----------------------------------------------------------------------=== #

from std.collections import Set
from std.math import rsqrt
from std.random import random_ui64, seed
from std.sys import get_defined_dtype, get_defined_int

from max.benchmark import bencher_iter_custom
from std.benchmark import (
    Bench,
    Bencher,
    BenchId,
    BenchMetric,
    ThroughputMeasure,
)
from max.gpu.host import DeviceContext
from internal_utils import arg_parse
from kv_cache.types import (
    ContinuousBatchingKVCacheCollection,
    KVCacheStaticParams,
)
from layout import (
    Idx,
    Layout,
    LayoutTensor,
    RuntimeLayout,
    TileTensor,
    UNKNOWN_VALUE,
    row_major,
)
from layout._fillers import random
from nn.attention.gpu.mha import flash_attention
from nn.attention.mha_mask import CausalMask

from std.utils import IndexList


def _get_run_name[
    dtype: DType,
    num_q_heads: Int,
    num_kv_heads: Int,
    head_dim: Int,
](
    batch_size: Int,
    seq_len: Int,
    use_random_seq_lengths: Bool,
    cache_len: Int,
    use_random_cache_lengths: Bool,
) -> String:
    # fmt: off
    return String(
        "fused_qkv_ragged_flash_attention(", dtype, ") : "

        # head_info
        "num_q_heads=", num_q_heads, ", ",
        "num_kv_heads=", num_kv_heads, ", ",
        "head_dim=", head_dim, " : ",

        "batch_size=", batch_size, ", ",
        "seq_len=", seq_len, ", ",
        "use_random_seq_lengths=", use_random_seq_lengths, ", ",
        "cache_len=", cache_len, ", ",
        "use_random_cache_lengths=", use_random_cache_lengths
    )
    # fmt: on


def execute_kv_cache_ragged_flash_attention[
    dtype: DType,
    head_dim: Int,
    num_q_heads: Int,
    num_kv_heads: Int,
](
    ctx: DeviceContext,
    mut m: Bench,
    batch_size: Int,
    seq_len: Int,
    use_random_seq_lengths: Bool,
    cache_len: Int,
    use_random_cache_lengths: Bool,
) raises:
    comptime num_layers = 1
    comptime layer_idx = 0

    var num_blocks = batch_size * 2
    comptime CollectionType = ContinuousBatchingKVCacheCollection[
        dtype,
        KVCacheStaticParams(num_heads=num_kv_heads, head_size=head_dim),
        ...,
    ]

    debug_assert(
        batch_size < num_blocks,
        "batch_size passed to unit test (",
        batch_size,
        ") is larger than configured num_blocks (",
        num_blocks,
        ")",
    )

    # Create device buffers for row offsets and cache lengths
    var input_row_offsets_device = ctx.enqueue_create_buffer[DType.uint32](
        batch_size + 1
    )
    var cache_lengths_device = ctx.enqueue_create_buffer[DType.uint32](
        batch_size
    )

    var max_context_length: UInt32 = 0
    var max_seq_length: UInt32 = 0
    var total_seq_len: UInt32 = 0
    var flop_count = 0

    # Initialize row offsets and cache lengths on host
    with input_row_offsets_device.map_to_host() as row_offsets_host:
        with cache_lengths_device.map_to_host() as cache_lengths_host:
            for i in range(batch_size):
                var curr_seq_length: UInt32
                if use_random_seq_lengths:
                    curr_seq_length = random_ui64(1, UInt64(seq_len)).cast[
                        DType.uint32
                    ]()
                else:
                    curr_seq_length = UInt32(seq_len)

                var curr_cache_length: UInt32
                if use_random_cache_lengths:
                    curr_cache_length = random_ui64(1, UInt64(cache_len)).cast[
                        DType.uint32
                    ]()
                else:
                    curr_cache_length = UInt32(cache_len)

                max_context_length = max(
                    max_context_length, curr_cache_length + curr_seq_length
                )
                max_seq_length = max(max_seq_length, curr_seq_length)

                row_offsets_host[i] = total_seq_len
                cache_lengths_host[i] = curr_cache_length
                total_seq_len += curr_seq_length

                flop_count += (
                    4
                    * num_q_heads
                    * Int(curr_cache_length + curr_seq_length)
                    * Int(curr_seq_length)
                    * head_dim
                )

            row_offsets_host[batch_size] = total_seq_len

    # Q tensor [total_seq_len, num_q_heads, head_dim]
    var q_device = ctx.enqueue_create_buffer[dtype](
        Int(total_seq_len) * num_q_heads * head_dim
    )

    # Initialize Q with random data
    with q_device.map_to_host() as q_host:
        var q_host_tensor = TileTensor(
            q_host,
            row_major(
                (
                    total_seq_len,
                    Idx[num_q_heads],
                    Idx[head_dim],
                )
            ),
        )
        random(q_host_tensor)

    # Create Q tensor
    var q_tensor = TileTensor(
        q_device,
        row_major((total_seq_len, Idx[num_q_heads], Idx[head_dim])),
    )

    # Output tensor [total_seq_len, num_q_heads, head_dim]
    var output_device = ctx.enqueue_create_buffer[dtype](
        Int(total_seq_len) * num_q_heads * head_dim
    )
    var output_device_tensor = TileTensor(
        output_device,
        row_major((total_seq_len, Idx[num_q_heads], Idx[head_dim])),
    )

    # KV block tensor [num_blocks, 2, num_layers, seq_len+cache_len, num_kv_heads, head_dim]
    comptime kv_block_layout = Layout.row_major[6]()
    var kv_block_shape = IndexList[6](
        num_blocks,
        2,
        num_layers,
        seq_len + cache_len,
        num_kv_heads,
        head_dim,
    )
    var kv_block_runtime = RuntimeLayout[kv_block_layout].row_major(
        kv_block_shape
    )

    var kv_block_device = ctx.enqueue_create_buffer[dtype](
        kv_block_shape.flattened_length()
    )

    # Initialize KV block with random data
    with kv_block_device.map_to_host() as kv_block_host:
        var kv_block_host_tensor = LayoutTensor[dtype, kv_block_layout](
            kv_block_host, kv_block_runtime
        )
        random(kv_block_host_tensor)

    var kv_block_tensor = LayoutTensor[dtype, kv_block_layout](
        kv_block_device, kv_block_runtime
    )

    # Lookup table [batch_size]
    comptime lookup_layout = Layout.row_major(UNKNOWN_VALUE)
    var lookup_shape = IndexList[1](batch_size)
    var lookup_runtime = RuntimeLayout[lookup_layout].row_major(lookup_shape)

    var lookup_table_device = ctx.enqueue_create_buffer[DType.uint32](
        lookup_shape.flattened_length()
    )

    # Initialize lookup table with random block indices
    with lookup_table_device.map_to_host() as lookup_host:
        var block_idx_set = Set[Int]()
        var idx = 0
        while idx < batch_size:
            var randval = Int(random_ui64(0, UInt64(num_blocks - 1)))
            if randval in block_idx_set:
                continue

            block_idx_set.add(randval)
            lookup_host[idx] = UInt32(randval)
            idx += 1

    # Create tensors for row offsets, cache lengths, and lookup table
    var input_row_offsets_tensor = TileTensor(
        input_row_offsets_device, row_major(batch_size + 1)
    )

    comptime cache_lengths_lt_layout = Layout(UNKNOWN_VALUE)
    var cache_lengths_tensor = LayoutTensor[
        DType.uint32, cache_lengths_lt_layout
    ](
        cache_lengths_device,
        RuntimeLayout[cache_lengths_lt_layout].row_major(
            IndexList[1](batch_size)
        ),
    )
    var lookup_table_tensor = LayoutTensor[DType.uint32, lookup_layout](
        lookup_table_device, lookup_runtime
    )

    var kv_collection_device = CollectionType(
        LayoutTensor[dtype, Layout.row_major[6](), MutAnyOrigin](
            kv_block_tensor.ptr.as_unsafe_any_origin(),
            RuntimeLayout[Layout.row_major[6]()](
                kv_block_tensor.runtime_layout.shape.value,
                kv_block_tensor.runtime_layout.stride.value,
            ),
        ),
        LayoutTensor[DType.uint32, Layout(UNKNOWN_VALUE), ImmutAnyOrigin](
            cache_lengths_tensor.ptr.as_imm().as_unsafe_any_origin(),
            RuntimeLayout[Layout(UNKNOWN_VALUE)](
                cache_lengths_tensor.runtime_layout.shape.value,
                cache_lengths_tensor.runtime_layout.stride.value,
            ),
        ),
        LayoutTensor[DType.uint32, Layout(UNKNOWN_VALUE), ImmutAnyOrigin](
            lookup_table_tensor.ptr.as_imm().as_unsafe_any_origin(),
            RuntimeLayout[Layout(UNKNOWN_VALUE)](
                lookup_table_tensor.runtime_layout.shape.value,
                lookup_table_tensor.runtime_layout.stride.value,
            ),
        ),
        max_seq_length,
        max_context_length,
    )

    var k_cache_device = kv_collection_device.get_key_cache(layer_idx)
    var v_cache_device = kv_collection_device.get_value_cache(layer_idx)

    @always_inline
    def bench_func(
        mut b: Bencher,
    ) raises {
        var q_tensor,
        var k_cache_device,
        var v_cache_device,
        var output_device_tensor,
        var input_row_offsets_tensor,
        imm,
    }:
        @always_inline
        def kernel_launch(ctx: DeviceContext) raises {imm}:
            flash_attention[ragged=True](
                output_device_tensor.to_layout_tensor().as_unsafe_any_origin(),
                q_tensor.to_layout_tensor(),
                k_cache_device,
                v_cache_device,
                CausalMask(),
                input_row_offsets_tensor.to_layout_tensor(),
                rsqrt(Float32(head_dim)),
                ctx,
            )

        bencher_iter_custom(b, kernel_launch, ctx)

    m.bench_function(
        bench_func,
        BenchId(
            _get_run_name[dtype, num_q_heads, num_kv_heads, head_dim](
                batch_size,
                seq_len,
                use_random_seq_lengths,
                cache_len,
                use_random_cache_lengths,
            )
        ),
        [ThroughputMeasure(BenchMetric.flops, flop_count)],
    )


def main() raises:
    comptime dtype = get_defined_dtype["dtype", DType.bfloat16]()

    comptime head_dim = get_defined_int["head_dim", 128]()
    comptime num_q_heads = get_defined_int["num_q_heads", 32]()
    comptime num_kv_heads = get_defined_int["num_kv_heads", 8]()

    var batch_size = arg_parse("batch_size", 1)
    var use_random_seq_lengths = arg_parse("use_random_seq_lengths", False)
    var seq_len = arg_parse("seq_len", 1)
    var cache_len = arg_parse("cache_len", 1)
    var use_random_cache_lengths = arg_parse("use_random_cache_lengths", False)

    seed(0)

    var m = Bench()
    with DeviceContext() as ctx:
        # benchmarking flash attention
        execute_kv_cache_ragged_flash_attention[
            dtype,
            head_dim,
            num_q_heads,
            num_kv_heads,
        ](
            ctx,
            m,
            batch_size,
            seq_len,
            use_random_seq_lengths,
            cache_len,
            use_random_cache_lengths,
        )

    m.dump_report()
